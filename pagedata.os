// /*----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------*/
// Идея интерпретатора https://github.com/tsukanov-as/kojura

Перем БазаДанных Экспорт;
Перем ИмяДанных Экспорт;
Перем ПозицияДанных Экспорт;

Перем Данные;
Перем КодУзла;
Перем Узлы Экспорт;
//Перем Изменены Экспорт;
Перем сКоличество Экспорт;
Перем Количество Экспорт;
Перем Пустой Экспорт;
Перем Очередь;
Перем Состояния;
Перем ОбъектыОбновить Экспорт;
Перем Представление Экспорт;
Перем Рефлектор;
Перем Процесс Экспорт;
Перем Корень Экспорт;
Перем К Экспорт;
Перем НетЗначения Экспорт;
Перем ВсеСвязи;

// Функция УзелСостояние(Узел, СостояниеИмя) Экспорт
// 	Перем УзелСостояния, УзелСостояние;
// 	Если Узел.Свойство("Состояния", УзелСостояния) Тогда
// 		УзелСостояния.Свойство(СостояниеИмя, УзелСостояние);
// 	КонецЕсли;
// 	Возврат УзелСостояние;
// КонецФункции // УзелСостояние(Узел)


Функция СтрокуВСтруктуру(Знач Стр)
	Стр = СтрРазделить(Стр, Символы.Таб);
	Ключ = Неопределено;
	Рез = Новый Структура;
	Для Каждого знСтр Из Стр Цикл
		Если Ключ = Неопределено Тогда
			Ключ = знСтр;
		Иначе
			Рез.Вставить(Ключ, знСтр);
			Ключ = Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат Рез;
КонецФункции


Функция СвойстваВСтуктуру(УзелСвойства) Экспорт
	Результат = Новый Структура;
	св = УзелСвойства.Дочерний;
	Пока НЕ св = Неопределено Цикл
		ИмяСвойства = св.Имя;
		Если Прав(ИмяСвойства, 1) = "." Тогда // группа свойств
			ИмяСвойства = Лев(ИмяСвойства, СтрДлина(ИмяСвойства) - 1);
			Результат.Вставить(ИмяСвойства, СвойстваВСтуктуру(св));
		Иначе
			Результат.Вставить(ИмяСвойства, ЗначениеСвойства(св));
		КонецЕсли;
		св = св.Соседний;
	КонецЦикла;
	Возврат Результат;
КонецФункции


Функция ИмяЗначение(Имя = "", Значение = "")
	Возврат Новый Структура("Имя, Значение", Имя, Значение);
КонецФункции


// Функция УзелСостояниеЗначение(Узел, СостояниеИмя, Знач СостояниеЗначение) Экспорт
// 	Перем УзелСостояния;
// 	Если НЕ Узел.Свойство("Состояния", УзелСостояния) Тогда
// 		УзелСостояния = Новый Структура();
// 		Узел.Вставить("Состояния", УзелСостояния);
// 	КонецЕсли;
// 	УзелСостояния.Вставить(СостояниеИмя, СостояниеЗначение);
// 	//Сообщить("" + Узел.Код + " " + СостояниеИмя + "=" + (Лев(СостояниеЗначение,30)));
// 	Возврат СостояниеЗначение;
// КонецФункции // УзелСостояниеЗначение(Узел)


Функция СтруктуруВСтроку(знСтруктура) Экспорт
	Если НЕ ТипЗнч(знСтруктура) = Тип("Структура") Тогда
		Возврат знСтруктура;
	КонецЕсли;
	Результат = "";
	Для каждого Элемент Из знСтруктура Цикл
		Ключ = Элемент.Ключ;
		Значение = Элемент.Значение;

		Если Ключ = "Код" ИЛИ Ключ = "Старший" ИЛИ Ключ = "Родитель" Тогда
			Продолжить;
		ИначеЕсли Ключ = "Имя" Тогда
			Ключ = "И";
		ИначеЕсли Ключ = "Значение" Тогда
			Ключ = "З";
			Значение = СтрЗаменить(Значение, Символы.Таб, "#x9");
			Значение = СтрЗаменить(Значение, Символы.ПС, "#xA");
			Значение = СтрЗаменить(Значение, Символы.ВК, "#xD");
		// ИначеЕсли Ключ = "сДочерний" Тогда
		// 	Продолжить;
		ИначеЕсли "" + Значение = "" Тогда
			Продолжить;
		ИначеЕсли Ключ = "Дочерний" Тогда
			Значение = Значение.Код;
			Если Лев(Значение, 1) = "s" Тогда
				Продолжить;
			КонецЕсли;
			Ключ = "Д";
		ИначеЕсли Ключ = "Соседний" Тогда
			Значение = Значение.Код;
			Если Лев(Значение, 1) = "s" Тогда
				Продолжить;
			КонецЕсли;
			Ключ = "С";
		ИначеЕсли Ключ = "Атрибут" Тогда
			Значение = Значение.Код;
			Ключ = "А";
		Иначе
			Продолжить;
		КонецЕсли;
		Результат = Результат + ?(Результат = "", "", Символы.Таб) + Ключ + Символы.Таб + Значение;
	КонецЦикла;
	Возврат Результат;
КонецФункции


Функция УзелСвойство(Узел, Свойство) Экспорт
	УзелСвойство = Неопределено;
	Если НЕ Узел = Неопределено Тогда
		Узел.Свойство(Свойство, УзелСвойство);
	КонецЕсли;
	Возврат УзелСвойство;
КонецФункции // УзелСвойство(Узел)


Функция ЗначениеСвойства(знач УзелСвойство, Имя = Неопределено) Экспорт
	Значение = Неопределено;
	Если НЕ Имя = Неопределено Тогда
		УзелСвойство = УзелСвойство(УзелСвойство, Имя);
	КонецЕсли;
	Если НЕ УзелСвойство = Неопределено Тогда
		ДочернийУзел = УзелСвойство.Дочерний;
		Если НЕ ДочернийУзел = Неопределено Тогда
			Значение = Интерпретировать(ДочернийУзел, , Ложь);
		Иначе
			Значение = "" + УзелСвойство(УзелСвойство, "Значение");
		КонецЕсли;
	КонецЕсли;
	Возврат Значение;
КонецФункции // ЗначениеСвойства()


Функция ПоказатьУзел(Знач Узел, Атрибуты = "", Дочерний = "", ЭтоАтрибут = Ложь) Экспорт
	Перем Представление;

	Представление = "";

	УзелИмя = Узел.Имя;
	УзелЗначение = "";
	Если Узел.Свойство("Значение") Тогда
		УзелЗначение = 	Узел.Значение;
	КонецЕсли;

	Если ЭтоАтрибут Тогда

		Если НЕ Дочерний = "" Тогда
			УзелЗначение = Дочерний;
		КонецЕсли;

		Если УзелИмя = "Строка" Тогда
			Представление = " " + УзелЗначение;
		Иначе
			АтрибутИмя = СтрЗаменить(УзелИмя, "xml_lang", "xml:lang");
			АтрибутИмя = СтрЗаменить(АтрибутИмя, "_", "-");
			Представление = " " + АтрибутИмя + "=""" + УзелЗначение + """";
		КонецЕсли;

	Иначе

		Если Узел.Имя = "comment" Тогда
			Представление = "<!-- " + УзелЗначение + " -->";
		ИначеЕсли Узел.Имя = "br" Тогда
			Представление = "<" + Узел.Имя + ">";
		Иначе
			Представление = "<" + УзелИмя + Атрибуты + " id='" + "_" + Узел.Код + "'>";
			Представление = Представление + УзелЗначение;
			Представление = Представление + Дочерний + "</" + УзелИмя + ">";
		КонецЕсли;

	КонецЕсли;

	Возврат Представление;

КонецФункции // ПоказатьУзел()


// Создать копию ветки
Функция КопироватьВетку(Узел, Цель = Неопределено, Старший, Родитель, ЭтоАтрибут = Ложь, ПервыйВызов = Истина, Служебный = Истина, ПараметрыЗамены = Неопределено) Экспорт

	ИмяУзла = УзелСвойство(Узел, "Имя");
	ЗначениеУзла = УзелСвойство(Узел, "Значение");

	Если Цель = Неопределено Тогда
		Цель = ЭтотОбъект;
	КонецЕсли;

	КопияУзел = Цель.НовыйУзел(Новый Структура("Имя, Значение, Старший, Родитель", ИмяУзла, ЗначениеУзла, Старший, Родитель), Служебный ИЛИ Служебный(Узел));

	Если НЕ ЭтоАтрибут Тогда

		Если НЕ Узел.Атрибут = Неопределено Тогда
			УзелАтрибут = КопироватьВетку(Узел.Атрибут, Цель, КопияУзел, КопияУзел, Истина, Ложь, Служебный ИЛИ Служебный(Узел.Атрибут), ПараметрыЗамены);
			КопияУзел.Вставить("Атрибут", УзелАтрибут);
		КонецЕсли;

	КонецЕсли;

	Если Узел.Дочерний = Неопределено Тогда
		Если ИмяУзла = "Узел" Тогда
			Если НЕ ПараметрыЗамены = Неопределено Тогда
				Параметр = УзелСвойство(ПараметрыЗамены, ЗначениеУзла);
				Если ТипЗнч(Параметр) = Тип("Структура") Тогда
					Узел.Дочерний = Параметр;
				ИначеЕсли ТипЗнч(Параметр) = Тип("Строка") Тогда
					КопияУзел.Вставить("Дочерний", Цель.НовыйУзел(Новый Структура("Имя, Значение, Старший, Родитель", "Строка", Параметр, Узел, Узел), Служебный ИЛИ Служебный(Узел)));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если НЕ Узел.Дочерний = Неопределено Тогда
		УзелДочерний = КопироватьВетку(Узел.Дочерний, Цель, КопияУзел, КопияУзел, , Ложь, Служебный ИЛИ Служебный(Узел.Дочерний), ПараметрыЗамены);
		КопияУзел.Вставить("Дочерний", УзелДочерний);
	КонецЕсли;

	Если ПервыйВызов Тогда
		КопияУзел.Вставить("Соседний", Неопределено);
	Иначе
		Если НЕ Узел.Соседний = Неопределено Тогда
			УзелСоседний = КопироватьВетку(Узел.Соседний, Цель, КопияУзел, Родитель, ЭтоАтрибут, Ложь, Служебный ИЛИ Служебный(Узел.Соседний), ПараметрыЗамены);
			КопияУзел.Вставить("Соседний", УзелСоседний);
		КонецЕсли;
	КонецЕсли;

	Возврат КопияУзел;

КонецФункции // КопироватьВетку()


Функция СравнитьИмя(Узел, Тип, Имя)
	Если Узел.Имя = "О" ИЛИ Узел.Имя = "Объект" Тогда
		зн = "" + УзелСвойство(Узел, "Значение");
		Если НЕ зн = "" Тогда
			м = СтрРазделить(зн, " ");
			оТип = м[0];
			оИмя = м[0];
			Если м.Количество() = 2 Тогда // полный путь
				оИмя = м[1];
			КонецЕсли;
			Если ((Тип = Имя) И (оТип = Тип ИЛИ оИмя = Имя)) ИЛИ
				((оТип = Тип ИЛИ Тип = "*") И (оИмя = Имя ИЛИ Имя = "*")) Тогда
					Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Ложь;
КонецФункции // СравнитьИмя()


Функция НайтиОбъекты(Знач Узел, оПоз, оТип, оИмя)

	Результаты = Новый Массив;

	Пока НЕ Узел = Неопределено Цикл
		Если Узел.Имя = "О" ИЛИ Узел.Имя = "Объект" Тогда
			Прервать;
		КонецЕсли;
		Узел = Узел.Родитель;
	КонецЦикла;

	Если оПоз = "" Тогда // свой объект
		Результаты.Добавить(Узел);
		Возврат Результаты;
	ИначеЕсли оПоз = "р" Тогда // найти родителя
		Узел = Узел.Родитель;
		Пока НЕ Узел = Неопределено Цикл
			Если СравнитьИмя(Узел, оТип, оИмя) Тогда
				Результаты.Добавить(Узел);
				Возврат Результаты;
			КонецЕсли;
			Узел = Узел.Родитель;
		КонецЦикла;
		Возврат Узел;
	ИначеЕсли оПоз = "д" Тогда // найти дочернего
		Узел = Узел.Дочерний;
	ИначеЕсли оПоз = "с" Тогда // найти соседнего
		Узел = Узел.Родитель.Дочерний;
	КонецЕсли;

	Пока НЕ Узел = Неопределено Цикл
		Если СравнитьИмя(Узел, оТип, оИмя) Тогда
			Результаты.Добавить(Узел);
		КонецЕсли;
		Узел = Узел.Соседний;
	КонецЦикла;

	Возврат Результаты; //Узел;

КонецФункции // НайтиОбъекты(Узел, Путь)


Функция СтруктураСвойств(УзелСвойства) Экспорт
	// св = Дочерний(УзелСвойства);
	// Пока НЕ св = Неопределено Цикл
	// 	ИмяСвойства = св.Имя;
	// 	Если Прав(ИмяСвойства, 1) = "." Тогда // группа свойств
	// 		СтруктураСвойств(св);
	// 	КонецЕсли;
	// 	св = Соседний(св);
	// КонецЦикла;
	Возврат УзелСвойства;
КонецФункции // СтруктураСвойств()


Функция СвойствоОбъекта(Знач Узел, ИмяСвойства)
	Свойство = Неопределено;
	Если НЕ Узел = Неопределено Тогда
		Свойство = Узел.Дочерний;
		МассивСвойства = СтрРазделить(ИмяСвойства, ".");
		к = 0;
		Пока к < МассивСвойства.Количество() Цикл
			ИмяСвойства = МассивСвойства[к];
			св = Свойство.Дочерний;

			Пока НЕ св = Неопределено Цикл
				ИмяУзла = св.Имя;
				Если Прав(ИмяУзла, 1) = "." Тогда
					ИмяУзла = Лев(ИмяУзла, СтрДлина(ИмяУзла) - 1);
				КонецЕсли;
				Если ИмяСвойства = ИмяУзла Тогда
					Прервать;
				КонецЕсли;
				св = св.Соседний;
			КонецЦикла;

			Свойство = св;
			Если Свойство = Неопределено Тогда
				Прервать;
			КонецЕсли;

			к = к + 1;
		КонецЦикла;
	КонецЕсли;
	Возврат Свойство;
КонецФункции // СвойствоОбъекта()


Функция ПолучитьСвойстваПоСсылке(Знач Узел, Путь = "", СоздатьСвязь = Ложь) Экспорт

	Результаты = Новый Массив;
	НачальныйУзел = Узел;

	Если ТипЗнч(Путь) = Тип("Строка") Тогда

		Если Путь = "" Тогда
			Путь = "" + УзелСвойство(Узел, "Значение");
			Если Путь = "" Тогда
				Если НЕ Узел.Дочерний = Неопределено Тогда
					Путь = "" + Интерпретировать(Узел.Дочерний, , Ложь);
				КонецЕсли;
				Если Путь = "" Тогда
					Возврат Неопределено;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		МассивПуть = СтрРазделить(Путь, " ");
		оПоз = "";
		оТип = "*";
		оИмя = "*";
		Если МассивПуть.Количество() = 4 Тогда // полный путь
			оПоз = МассивПуть[0];
			оТип = МассивПуть[1];
			оИмя = МассивПуть[2];
		ИначеЕсли МассивПуть.Количество() = 3 Тогда // без типа объекта
			оПоз = МассивПуть[0];
			оТип = МассивПуть[1];
			оИмя = МассивПуть[1];
		ИначеЕсли МассивПуть.Количество() = 2 Тогда // без типа и имени объекта
			оПоз = МассивПуть[0];
		КонецЕсли;

		Если оПоз = "у" Тогда // указатель на объект или свойство
			Узел = ПолучитьУзел(МассивПуть[1]);
			Если НЕ Узел = Неопределено Тогда
				Объекты = Новый Массив();
				Если МассивПуть.Количество() = 3 Тогда // Указано имя свойства
					Объекты.Добавить(Узел);
				Иначе
					Результаты.Добавить(Узел);
				КонецЕсли;
			КонецЕсли;
		Иначе // найти по маске
			Объекты = НайтиОбъекты(Узел, оПоз, оТип, оИмя);
			Если Объекты.Количество() = 0 Тогда
				Сообщить("Объекты не найдены: " + Путь);
			КонецЕсли;

		КонецЕсли;

		Для каждого оУзел Из Объекты Цикл
			ИмяСвойства = МассивПуть[МассивПуть.Количество()-1];
			Узел = СвойствоОбъекта(оУзел, ИмяСвойства);
			Если Узел = Неопределено Тогда
				Сообщить("Свойство не найдено: " + Путь);
				Продолжить;
			КонецЕсли;
			Результаты.Добавить(Узел);
		КонецЦикла;

	Иначе

		Результаты.Добавить(Путь); // только чтобы создать связь

	КонецЕсли;


	Если СоздатьСвязь = Истина Тогда

		Для каждого Узел Из Результаты Цикл

			// объект и свойство которые нужно добавить в связи для обновления
			нОбъект = НачальныйУзел;
			нСвойство = НачальныйУзел;
			Пока НЕ нОбъект = Неопределено Цикл
				Если нСвойство = НачальныйУзел И Прав(нОбъект.Родитель.Имя, 1) = "." Тогда // группа свойств
					нСвойство = нОбъект;
				КонецЕсли;
				Если нОбъект.Имя = "О" ИЛИ нОбъект.Имя = "Объект" Тогда
					Прервать;
				КонецЕсли;
				нОбъект = нОбъект.Родитель;
			КонецЦикла;

			оУзел = Узел;
			Пока НЕ оУзел = Неопределено Цикл
				Если оУзел.Имя = "О" ИЛИ оУзел.Имя = "Объект" Тогда
					Прервать;
				КонецЕсли;
				оУзел = оУзел.Родитель;
			КонецЦикла;

			Если НЕ оУзел = нОбъект Тогда // свои свойства не обновлять
				Связи = ВсеСвязи.Получить(Узел); // связанные свойства
				Если Связи = Неопределено Тогда
					Связи = Новый Соответствие;
					ВсеСвязи.Вставить(Узел, Связи);
				КонецЕсли;
				//Сообщить("+Связи " + Узел.Код + " -> " + нОбъект.Код + " св. " + нСвойство.Код);
				Связи.Вставить(нСвойство, нОбъект);
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

	Возврат Результаты;

КонецФункции // ПолучитьСвойстваПоСсылке()


Функция ОбновитьПредставление(Узел = Неопределено) Экспорт

	Попытка
		Если НЕ Узел = Неопределено Тогда
			Очередь.Добавить(Узел);
		ИначеЕсли ОбъектыОбновить.Количество() Тогда
			Для каждого элОбъект Из ОбъектыОбновить Цикл
				Узел = элОбъект.Ключ;
				Если элОбъект.Значение = Истина Тогда // оптимизировать порядок обновления
					рУзел = Узел;
					Пока НЕ рУзел = Неопределено Цикл
						Если ОбъектыОбновить.Получить(рУзел) = Истина Тогда
							Продолжить;
						КонецЕсли;
						рУзел = рУзел.Родитель;
					КонецЦикла;
					Если рУзел = Неопределено Тогда
						Очередь.Добавить(Узел);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			ОбъектыОбновить.Очистить();
		КонецЕсли;

		НачалоЦикла = ТекущаяУниверсальнаяДатаВМиллисекундах();

		Представление = "";

		Пока Очередь.Количество() Цикл

			Узел = Очередь.Получить(Очередь.Количество() - 1);
			Состояние = Состояния.Получить(Узел);

			Если Состояние = Неопределено Тогда

				Если ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЦикла > 100 Тогда
					Сообщить("Длина очереди " + Очередь.Количество());
					Прервать;
				КонецЕсли;

				Имя = Узел.Имя;
				Значение = Неопределено;
				Узел.Свойство("Значение", Значение);

				Сообщить("На очереди у " + Узел.Код + " " + Имя + " " + Значение);

				Если Имя = "Истина" Тогда
					Состояние = Истина;
				ИначеЕсли Имя = "Ложь" Тогда
					Состояние = Ложь;
				// ИначеЕсли Имя = "Неопределено" Тогда
				// 	Состояние = Неопределено;
				ИначеЕсли Имя = "Пустой" Тогда
					Состояние = Пустой;
				ИначеЕсли Имя = "Число" Тогда
					Если НЕ "" + Значение = "" Тогда
						Состояние = Число(Значение);
					Иначе
						УзелДочерний = Узел.Дочерний;
						Если НЕ Узел.Дочерний = Неопределено Тогда
							Состояние = Состояния.Получить(Узел.Дочерний);
							Если Состояние = Неопределено Тогда
								Очередь.Добавить(УзелДочерний);
								Продолжить;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли Имя = "Пробел" Тогда
					Состояние = " ";
				ИначеЕсли Имя = "Источник" Тогда
					Состояние = Процесс.Источник;
				ИначеЕсли Имя = "Процесс" Тогда
					Состояние = Процесс.procid;
				ИначеЕсли Имя = "Строка" Тогда
					Если НЕ Узел.Свойство("ЭтоАтрибут") = Истина Тогда
						Состояние = "" + Значение;
						Если НЕ Узел.Дочерний = Неопределено Тогда
							УзелДочерний = Узел.Дочерний;
							Пока НЕ УзелДочерний = Неопределено Цикл
								сДочерний = Состояния.Получить(УзелДочерний);
								Если сДочерний = Неопределено Тогда
									Очередь.Добавить(УзелДочерний);
									Продолжить;
								КонецЕсли;
								Состояние = Состояние + сДочерний;
								УзелДочерний = УзелДочерний.Соседний;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли Имя = "Структура" Тогда
					Состояние = Значение;

				ИначеЕсли (Имя = "Аргумент" ИЛИ Имя = "А") Тогда
					Если Узел.Свойство("ЭтоАтрибут") = Истина Тогда
						// объявление аргумента
						Если Значение = "Значение" ИЛИ Значение = "Выбрано" Тогда // текстовое поле
							Если НЕ Узел.Дочерний = Неопределено Тогда
								сДочерний = Состояния.Получить(Узел.Дочерний);
								Если сДочерний = Неопределено Тогда
									Очередь.Добавить(Узел.Дочерний);
									Продолжить;
								КонецЕсли;
								Если Значение = "Значение" Тогда // текстовое поле
									Состояние = " value=""" + сДочерний + """ onchange=""addcmd(this,event)""";
								ИначеЕсли Значение = "Выбрано" Тогда // галочка
									Состояние = ?(сДочерний = "true", " checked=""checked""", "");
								КонецЕсли;
							КонецЕсли;
						ИначеЕсли Значение = "ПриИзменении" Тогда
							Состояние = " onchange=""addcmd(this,event)""";
						ИначеЕсли Значение = "ПриНажатии" Тогда
							Состояние = " onclick=""addcmd(this,event); return false""";
						ИначеЕсли Значение = "ПриОтправке" Тогда
							Состояние = " onsubmit=""addcmd(this,event); return false""";
						КонецЕсли;
					КонецЕсли;

				ИначеЕсли Имя = "Функция" ИЛИ Имя = "Ф" Тогда
					Если Узел.Дочерний = Неопределено Тогда
						Состояние = "" + ВызватьФункцию(Узел);
					КонецЕсли;
					Если НЕ Узел.Дочерний = Неопределено Тогда
						сДочерний = Состояния.Получить(Узел.Дочерний);
						Если сДочерний = Неопределено Тогда
							Очередь.Добавить(Узел.Дочерний);
							Продолжить;
						КонецЕсли;
						Состояние = Состояние + сДочерний;
					КонецЕсли;

				ИначеЕсли Имя = "Свойства." Тогда // свойства объекта
					Состояние = "";

				ИначеЕсли Имя = "Объект" ИЛИ Имя = "О" Тогда // это функциональный узел
					сОбъект = ОбработатьОбъект(Узел);
					Если сОбъект = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					Состояние = "" + сОбъект;

				ИначеЕсли Имя = "" Тогда // это оператор
					Параметры = Новый Массив;
					Параметры.Добавить(ЭтотОбъект);
					Параметры.Добавить(Узел.Дочерний);
					ИмяФункции = Значение;
					Если НЕ "" + Значение = "" Тогда
						Если СтрНайти(Значение, ".") Тогда
							ИмяФункции = СтрРазделить(ИмяФункции, ".");
							ИмяБиблиотеки = ИмяФункции[0];
							ИмяФункции = ИмяФункции[1];
						Иначе
							ИмяБиблиотеки = "Операторы";
						КонецЕсли;
						Библиотека = Процесс.ПолучитьБиблиотеку(ИмяБиблиотеки);
						Состояние = Рефлектор.ВызватьМетод(Библиотека, "Оператор_" + ИмяФункции, Параметры);
					КонецЕсли;

				ИначеЕсли Имя = "Ссылка" ИЛИ Имя = "С" Тогда
					Свойства = ПолучитьСвойстваПоСсылке(Узел, , Истина);
					Если Свойства.Количество() = 0 Тогда
						ВызватьИсключение "Свойство не найдено: " + Узел.Значение;
					КонецЕсли;
					Состояние = Свойства[0];

				ИначеЕсли Имя = "Переменная" ИЛИ Имя = "П" Тогда // значение переменной объекта
					сУзел = Узел;
					Пока НЕ сУзел = Неопределено Цикл
						Если сУзел.Имя = "Свойства." Тогда
							Состояние = "" + УзелСвойство(сУзел.Родитель, Значение);
							Прервать;
						КонецЕсли;
						сУзел = сУзел.Родитель;
					КонецЦикла;

				ИначеЕсли Имя = "Значение" ИЛИ Имя = "З" Тогда // значение свойств объектов
					Состояние = "";
					сУзел = Узел;
					Свойства = ПолучитьСвойстваПоСсылке(Узел, Значение, Истина);
					Если Свойства.Количество() = 0 Тогда
						Сообщить("Свойство не найдено: " + Значение + " узел " + Узел.Код);
					КонецЕсли;
					Для каждого Свойство Из Свойства Цикл
						Если Состояние = "" Тогда
							Состояние = ЗначениеСвойства(Свойство);
						Иначе
							Состояние = "" + Состояние + " " + ЗначениеСвойства(Свойство);
						КонецЕсли;
					КонецЦикла;

				ИначеЕсли Имя = "Свойство" ИЛИ Имя = "Атрибут" ИЛИ Имя = "Первый" ИЛИ Имя = "Соседний" Тогда

					Если НЕ Узел.Дочерний = Неопределено Тогда
						Элемент = Состояния.Получить(Узел.Дочерний);
						Если Элемент = Неопределено Тогда
							Очередь.Добавить(Узел.Дочерний);
							Продолжить;
						КонецЕсли;

						Если Имя = "Свойство" Тогда
							Состояние = УзелСвойство(Элемент, Значение);
						ИначеЕсли Имя = "Атрибут" Тогда
							Состояние = НайтиСоседний(Элемент.Атрибут, Значение).Значение;
						ИначеЕсли Имя = "Первый" Тогда
							Список = Элемент;
							Элемент = Список.Дочерний;
							Если Элемент = Неопределено Тогда
								Элемент = Пустой;
							КонецЕсли;
							Состояние = Элемент;
						ИначеЕсли Имя = "Соседний" Тогда
							Если НЕ Элемент = Неопределено Тогда
								Элемент = Элемент.Соседний;
							КонецЕсли;
							Если Элемент = Неопределено Тогда
								Элемент = Пустой;
							КонецЕсли;
							Состояние = Элемент;
						КонецЕсли;
					КонецЕсли;

				Иначе

					сАтрибут = "";
					Если НЕ Узел.Атрибут = Неопределено Тогда
						сАтрибут = Состояния.Получить(Узел.Атрибут);
						Если сАтрибут = Неопределено Тогда
							Узел.Атрибут.Вставить("ЭтоАтрибут", Истина);
							Очередь.Добавить(Узел.Атрибут);
						КонецЕсли;
					КонецЕсли;

					сДочерний = "";
					Если НЕ Узел.Дочерний = Неопределено Тогда
						сДочерний = Состояния.Получить(Узел.Дочерний);
						Если сДочерний = Неопределено Тогда
							Очередь.Добавить(Узел.Дочерний);
						КонецЕсли;
					КонецЕсли;

					сСоседний = "";
					Если НЕ Узел.Соседний = Неопределено Тогда
						сСоседний = Состояния.Получить(Узел.Соседний);
						Если сСоседний = Неопределено Тогда
							Если Узел.Свойство("ЭтоАтрибут") = Истина Тогда
								Узел.Соседний.Вставить("ЭтоАтрибут", Истина);
							КонецЕсли;
							Очередь.Добавить(Узел.Соседний);
						КонецЕсли;
					КонецЕсли;

					Если сАтрибут = Неопределено ИЛИ сДочерний = Неопределено ИЛИ сСоседний = Неопределено Тогда
						Продолжить;
					КонецЕсли;

					Состояние = "";

					Если Узел.Свойство("ЭтоАтрибут") = Истина Тогда

						Если НЕ сДочерний = "" Тогда
							Значение = сДочерний;
						КонецЕсли;

						Если Имя = "Строка" Тогда
							Состояние = " " + Значение;
						Иначе
							АтрибутИмя = СтрЗаменить(Имя, "xml_lang", "xml:lang");
							АтрибутИмя = СтрЗаменить(АтрибутИмя, "_", "-");
							Состояние = " " + АтрибутИмя + "=""" + Значение + """";
						КонецЕсли;

					Иначе

						Если Имя = "comment" Тогда
							Состояние = "<!-- " + Значение + " -->";
						ИначеЕсли Имя = "br" Тогда
							Состояние = "<" + Имя + ">";
						Иначе
							Состояние = "<" + Имя + сАтрибут + " id='" + "_" + Узел.Код + "'>";
							Состояние = Состояние + Значение;
							Состояние = Состояние + сДочерний + "</" + Имя + ">";
						КонецЕсли;

					КонецЕсли;

					Состояние = Состояние + сСоседний;

				КонецЕсли;

				Состояния.Вставить(Узел, Состояние);
				Представление = Представление + Состояние;

			КонецЕсли;

			Очередь.Удалить(Очередь.Количество() - 1);

		КонецЦикла;

		Если Очередь.Количество() Тогда
			Процесс.ЗадачаОбновитьВыполнить = Истина; // продолжение
		КонецЕсли;

	Исключение
		Процесс.ЗаписатьСобытие("Интерпретатор", ОписаниеОшибки(), 3);
		//Сообщить(ОписаниеОшибки());
		Инфо = ИнформацияОбОшибке();
		Стек = Инфо.ПолучитьСтекВызовов();
		Для каждого Кадр Из Стек Цикл
			Сообщить(Кадр.ИмяМодуля + " / " + Кадр.Метод + " / " + Кадр.НомерСтроки);
		КонецЦикла;
		//УзелСостояниеЗначение(элУзел.Значение, "Ошибка", Истина);
	КонецПопытки;

КонецФункции


Функция ОбновитьУзел(Знач Узел) Экспорт
	//Сообщить("ОбновитьУзел " + Узел.Код);
	ОбновитьСвязи(Узел);
	оУзел = Узел;
	Пока НЕ оУзел = Неопределено Цикл
		Если оУзел.Имя = "О" ИЛИ оУзел.Имя = "Объект" Тогда
			//Сообщить("Объект " + оУзел.Код);
			Изменения = УзелСвойство(оУзел, "Изменения");
			Если Изменения = Неопределено Тогда
				Изменения = Новый Соответствие;
				оУзел.Вставить("Изменения", Изменения);
				//Сообщить("+Изменения " + Узел.Код);
			КонецЕсли;
			Изменения.Вставить(Узел, Истина);
			ОбъектыОбновить.Вставить(оУзел, Истина);
			Процесс.ЗадачаОбновитьВыполнить = Истина; // продолжение
			Прервать;
		КонецЕсли;
		оУзел = оУзел.Родитель;
	КонецЦикла;
КонецФункции


Функция ПолучитьАргументы(Узел)
	Перем Значение;
	Аргументы = Новый Структура("ЭтотУзел", Узел);
	Аргумент = Узел.Атрибут;
	Пока НЕ Аргумент = Неопределено Цикл
		//УзелСостояниеЗначение(Аргумент, "ЭтоАтрибут", Истина); // для отслеживания источника изменений
		Если Аргумент.Имя = "Аргумент" ИЛИ Аргумент.Имя = "А" Тогда
			Аргументы.Вставить(Аргумент.Значение, Интерпретировать(Аргумент.Дочерний, , Ложь));
		ИначеЕсли Аргумент.Имя = "Свойства." Тогда // свойства объекта
			Аргументы.Вставить(Аргумент.Имя, Аргумент);
		ИначеЕсли Аргумент.Свойство("Значение", Значение) Тогда // тип значения аргумента - строка
			Аргументы.Вставить(Аргумент.Имя, Значение);
		Иначе
			Аргументы.Вставить(Аргумент.Имя, Аргумент.Дочерний);
		КонецЕсли;
		Аргумент = Аргумент.Соседний;
	КонецЦикла;
	Возврат Аргументы;
КонецФункции


Функция ДобавитьСобытие(Знач Узел, Событие, Значение = Неопределено) Экспорт
Перем зСвойство, уСвойство;

	Если Событие = "ПриНажатии" Тогда
		АтрибутПриНажатии = НайтиАтрибут(Узел, "А", "ПриНажатии"); // нужно вычислить и передать значение
		Если НЕ АтрибутПриНажатии = Неопределено Тогда
			уСвойство = АтрибутПриНажатии.Дочерний;
		КонецЕсли;
	ИначеЕсли Событие = "ПриИзменении" Тогда
		АтрибутЗначение = НайтиАтрибут(Узел, "А", "Значение"); // изменить значение свойства
		Если НЕ АтрибутЗначение = Неопределено Тогда
			зСвойство = АтрибутЗначение.Дочерний;
		КонецЕсли;
		АтрибутВыбрано = НайтиАтрибут(Узел, "А", "Выбрано"); // изменить значение свойства
		Если НЕ АтрибутВыбрано = Неопределено Тогда
			зСвойство = АтрибутВыбрано.Дочерний;
		КонецЕсли;
		АтрибутПриИзменении = НайтиАтрибут(Узел, "А", "ПриИзменении"); // нужно вычислить и передать значение
		Если НЕ АтрибутПриИзменении = Неопределено Тогда
			уСвойство = АтрибутПриИзменении.Дочерний;
		КонецЕсли;
	КонецЕсли;

	Если НЕ зСвойство = Неопределено Тогда
		Свойства = ПолучитьСвойстваПоСсылке(зСвойство);
		Для каждого Свойство Из Свойства Цикл
			НовоеЗначениеУзла(Свойство, ИмяЗначение("Строка", Значение), Служебный(Свойство), , Ложь);
		КонецЦикла;
	КонецЕсли;

	Если НЕ уСвойство = Неопределено Тогда
		Свойства = ПолучитьСвойстваПоСсылке(уСвойство);
		Для каждого Свойство Из Свойства Цикл
			уЗначение = уСвойство.Соседний;
			Если НЕ уЗначение = Неопределено Тогда
				Значение = Интерпретировать(уЗначение);
				//Значение = ЗначениеСвойства(сЗначение);
				НовоеЗначениеУзла(Свойство, Значение, Истина);
				Возврат Неопределено; // событие не создается
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если НЕ зСвойство = Неопределено Тогда
		Возврат Неопределено; // событие не создается
	КонецЕсли;

	стрСобытие = Новый Структура("Имя, Значение, Параметры", "" + Процесс.ПолучитьИД(), Событие + Символы.Таб + Узел.Код, Значение);

	УзелОбъект = Неопределено;
	Пока НЕ Узел = Неопределено Цикл
		Если Узел.Имя = "О" ИЛИ Узел.Имя = "Объект" Тогда
			УзелОбъект = Узел;
			Прервать;
		КонецЕсли;
		Узел = Узел.Родитель;
	КонецЦикла;
	Если НЕ УзелОбъект = Неопределено Тогда
		Свойства = Неопределено;
		свУзел = УзелОбъект.Дочерний;
		Если НЕ свУзел = Неопределено Тогда
			Если свУзел.Имя = "Свойства." Тогда
				Свойства = СтруктураСвойств(свУзел);
			КонецЕсли;
		КонецЕсли;
		Если НЕ Свойства = Неопределено Тогда
			Если Свойства.д.Свойство("События") Тогда
				НовоеЗначениеУзла(Свойства.д.События, стрСобытие, Истина, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецФункции // ДобавитьСобытие()


Функция ОбъектФорма(Узел, Свойства)
	Вид = "<script>var p = {id: '" + Узел.Код + "', fid: '" + Свойства.д.Форма.Код + "'";
	св = Свойства.д.Форма.Дочерний;
	Пока не св = Неопределено Цикл
		Вид = Вид + "," + св.Имя + ":" + св.Значение;
		св = св.Соседний;
	КонецЦикла;
	Возврат Вид + "}; var id='_" + Узел.Код + "'; updifrm(id,p);</script>";
КонецФункции // ОбъектФорма()


Функция ОбъектВид(Узел, Свойства)
	Вид = "" + ЗначениеСвойства(Свойства.д, "Описание");
	Возврат Вид + Узел.Содержимое;
КонецФункции


Функция СоздатьСвойства(Знач Узел, шСвойства, Служебный = Неопределено, вКонец = Неопределено) Экспорт
	св = Неопределено;
	Если Служебный = Неопределено Тогда
		Служебный = Служебный(Узел);
	КонецЕсли;
	к = -1;
	св = Узел;
	стрСвойства = СтрРазделить(шСвойства, Символы.ПС);
	Для каждого стр Из стрСвойства Цикл // парсер
		Если НЕ стр = "" Тогда
			м = СтрРазделить(стр, Символы.Таб);
			т = 0;
			Имя = "";
			Зн = "";
			п = 0;
			Пока п < м.Количество() Цикл
				Если м[п] = "" Тогда
					т = т + 1;
				ИначеЕсли Имя = "" Тогда
					Если т = 0 Тогда
						Служ = Служебный;
					КонецЕсли;
					имзн = СтрЗаменить(м[п], ": ", Символы.Таб);
					имзн = СтрРазделить(имзн, Символы.Таб);
					Имя = имзн[0];
					Если Лев(Имя, 1) = "*" Тогда
						Служ = Истина;
						Имя = Сред(Имя, 2);
					КонецЕсли;
					Если имзн.Количество() > 1 Тогда
						Зн = имзн[1];
					КонецЕсли;
				Иначе // атрибуты
					Прервать;
				КонецЕсли;
				п = п + 1;
			КонецЦикла;
			Если Служебный = "Только" Тогда
				Если НЕ Служ = Истина Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			Если Имя = "" И Зн = "" Тогда
				Продолжить;
			КонецЕсли;
			Если т > к Тогда
				св = НовыйДочерний(св, ИмяЗначение(Имя, "" + Зн), Служ, НЕ вКонец = Ложь И Служ = Истина);
				к = т;
			Иначе
				Пока т < к Цикл
					св = св.Родитель;
					к = к - 1;
				КонецЦикла;
				св = НовыйСоседний(св, ИмяЗначение(Имя, "" + Зн), Служ);
			КонецЕсли;
			// атрибуты
			ат = св;
			Пока п < м.Количество() Цикл
				имзн = СтрРазделить(м[п], "=");
				Имя = имзн[0];
				Если имзн.Количество() > 1 Тогда
					Зн = имзн[1];
				Иначе
					Зн = "";
				КонецЕсли;
				д = "";
				Если Лев(Зн, 1) = "|" Тогда // дочернее значение атрибута
					д = Сред(Зн, 2);
					Зн = "";
				КонецЕсли;
				Если ат = св Тогда
					ат = НовыйАтрибут(ат, ИмяЗначение(Имя, "" + Зн), Служ);
				Иначе
					ат = НовыйСоседний(ат, ИмяЗначение(Имя, "" + Зн), Служ);
				КонецЕсли;
				Если НЕ д = "" Тогда // добавить дочерний узел в атрибут
					имзн = СтрЗаменить(д, ": ", Символы.Таб);
					имзн = СтрРазделить(имзн, Символы.Таб);
					Имя = имзн[0];
					Если имзн.Количество() > 1 Тогда
						Зн = имзн[1];
					КонецЕсли;
					НовыйДочерний(ат, ИмяЗначение(Имя, "" + Зн), Служ, Служ = Истина);
				КонецЕсли;
				п = п + 1;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	Возврат Узел;
КонецФункции // СоздатьСвойства()


Функция ДобавитьСвойство(УзелСвойства, стрСвойства, Служебный = Ложь) Экспорт
	элСвойство = НовыйДочерний(УзелСвойства, стрСвойства, Служебный, Истина);
	УзелСвойства.д.Вставить(элСвойство.Имя, элСвойство);
	Возврат элСвойство;
КонецФункции // ДобавитьСвойство()


Функция ОбновитьСвязи(Знач Узел) Экспорт
	//Сообщить("ОбновитьСвязи " + Узел.Код);
	Связи = ВсеСвязи.Получить(Узел);
	Если НЕ Связи = Неопределено Тогда
		Для каждого элУзел Из Связи Цикл
			//Сообщить("+Изменения " + элУзел.Ключ.Код);
			Изменения = УзелСвойство(элУзел.Значение, "Изменения");
			Изменения.Вставить(элУзел.Ключ, Истина);
			//Сообщить("+ОбъектыОбновить " + элУзел.Значение.Код);
			ОбъектыОбновить.Вставить(элУзел.Значение, Истина);
		КонецЦикла;
	КонецЕсли;
КонецФункции


Функция ОбработатьОбъект(Знач Узел, Конструктор = Ложь) Экспорт

	ТипОбъекта = Узел.Значение;

	Если ТипОбъекта = "" Тогда
		Возврат Ложь;
	КонецЕсли;

	//Сообщить("ОбработатьОбъект " + Узел.Код + " " + ТипОбъекта);

	пТипИмя = Найти(ТипОбъекта, " ");
	Если пТипИмя > 0 Тогда
		ИмяОбъекта = Сред(ТипОбъекта, пТипИмя + 1);
		ТипОбъекта = Лев(ТипОбъекта, пТипИмя - 1);
	КонецЕсли;

	ТаблицаМетодов = Неопределено;

	Если СтрНайти(ТипОбъекта, ".") Тогда
		ТипОбъекта = СтрРазделить(ТипОбъекта, ".");
		ИмяБиблиотеки = ТипОбъекта[0];
		ТипОбъекта = ТипОбъекта[1];
	Иначе
		ИмяБиблиотеки = "Объекты";
	КонецЕсли;

	Библиотека = Процесс.ПолучитьБиблиотеку(ИмяБиблиотеки);

	Свойства = УзелСвойство(Узел, "Свойства");
	Если Свойства = Неопределено Тогда

		шСвойства = "";
		ИмяФункции = ТипОбъекта + "_Свойства";
		ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(Библиотека);
		Если Рефлектор.МетодСуществует(Библиотека, ИмяФункции) Тогда
			Параметры = Новый Массив;
			Параметры.Добавить(Узел);
			шСвойства = Рефлектор.ВызватьМетод(Библиотека, ИмяФункции, Параметры);
		КонецЕсли;

		свУзел = Узел.Дочерний;
		Если НЕ свУзел = Неопределено Тогда
			Если свУзел.Имя = "Свойства." Тогда
				Свойства = СтруктураСвойств(свУзел);
				Узел.Вставить("Свойства", Свойства);
				СоздатьСвойства(Свойства, шСвойства, "Только");
			КонецЕсли;
		КонецЕсли;

		Если Свойства = Неопределено Тогда // это новый объект
			Свойства = НовыйДочерний(Узел, ИмяЗначение("Свойства.", ""), Служебный(Узел));
			СоздатьСвойства(Свойства, шСвойства);
			Узел.Вставить("Свойства", Свойства);
		КонецЕсли;

	КонецЕсли;

	Если Конструктор Тогда
		Возврат Свойства;
	КонецЕсли;

	Изменения = УзелСвойство(Узел, "Изменения");
	Если Изменения = Неопределено Тогда
		// инициализация
		Изменения = Новый Соответствие;
		Изменения.Вставить(Узел, Истина);
		Узел.Вставить("ИмяОбъекта", ИмяОбъекта);
		Узел.Вставить("ТипОбъекта", ИмяОбъекта);
	КонецЕсли;

	ОбъектыОбновить.Удалить(Узел);

	Если Изменения.Количество() Тогда

		//Атрибуты = ПолучитьАргументы(Узел);
		Узел.Вставить("Изменения", Новый Соответствие);

		ИмяФункции = ТипОбъекта + "_Модель";
		Если Рефлектор.МетодСуществует(Библиотека, ИмяФункции) Тогда
			Сообщить("м. " + Узел.Код);
			Параметры = Новый Массив;
			Параметры.Добавить(ЭтотОбъект);
			Параметры.Добавить(Свойства);
			Параметры.Добавить(Изменения);
			Рефлектор.ВызватьМетод(Библиотека, ИмяФункции, Параметры);
			Для каждого св Из Изменения Цикл
				ОбновитьСвязи(св.Ключ);
			КонецЦикла;
		КонецЕсли;

	КонецЕсли;

	Если ПолучитьУзел(Узел.Код) = Неопределено Тогда // Узел был удален
		Возврат "";
	КонецЕсли;

	// обработать вложенные объекты
	ОбновитьВложение = Ложь;
	Содержимое = "";
	УзелДочерний = Узел.Дочерний.Соседний;
	Пока НЕ УзелДочерний = Неопределено Цикл
		сДочерний = Состояния.Получить(УзелДочерний);
		Если сДочерний = Неопределено Тогда
			Очередь.Добавить(УзелДочерний);
			ОбновитьВложение = Истина;
		КонецЕсли;
		Содержимое = Содержимое + сДочерний;
		УзелДочерний = УзелДочерний.Соседний;
	КонецЦикла;

	Если ОбновитьВложение Тогда
		Возврат Неопределено;
	КонецЕсли;

	Узел.Вставить("Содержимое", Содержимое);

	// сформировать представление объекта
	Состояние = "";
	Если Свойства.д.Свойство("Вид") Тогда // Стандартный вид
		Состояние = ЗначениеСвойства(Свойства.д.Вид);
	Иначе
		Состояние = ОбъектВид(Узел, Свойства);
		Состояние = "<div id='_" + Узел.Код + "'>" + Состояние + "</div>";
	КонецЕсли;
	Если Свойства.д.Свойство("Форма") Тогда
		Состояние = Состояние + ОбъектФорма(Узел, Свойства);
	КонецЕсли;

	Возврат Состояние;

КонецФункции // ОбработатьОбъект()


Функция ВызватьФункцию(Знач Узел, Знач ИмяФункции = Неопределено, структЗадача = Неопределено) Экспорт

	Если НЕ ИмяФункции = Неопределено Тогда // указано имя функции

		Если СтрНайти(ИмяФункции, ".") Тогда
			ИмяФункции = СтрРазделить(ИмяФункции, ".");
			ИмяБиблиотеки = ИмяФункции[0];
			ИмяФункции = ИмяФункции[1];
		Иначе
			ИмяБиблиотеки = "Функции";
		КонецЕсли;

		Библиотека = Процесс.ПолучитьБиблиотеку(ИмяБиблиотеки);
		Если НЕ Библиотека = Неопределено Тогда
			ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(Библиотека);
			Параметры = Неопределено;
			Для каждого Метод Из ТаблицаМетодов Цикл
				Если Метод.Имя = ИмяФункции Тогда
					Параметры = Новый Массив;
					Параметры.Добавить(ЭтотОбъект);
					Если структЗадача = Неопределено Тогда // если вызвал интерпретатор
						Аргументы = ПолучитьАргументы(Узел);
						Параметры.Добавить(Аргументы);
					Иначе // задача "ВыполнитьФункцию"
						Параметры.Добавить(структЗадача);
						Параметры.Добавить(Истина);
					КонецЕсли;
					Состояние = Рефлектор.ВызватьМетод(Библиотека, ИмяФункции, Параметры);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Параметры = Неопределено Тогда
				ВызватьИсключение "Функция " + ИмяФункции + " не найдена";
			КонецЕсли;
		Иначе
			ВызватьИсключение "Библиотека " + ИмяБиблиотеки + " не найдена";
		КонецЕсли;

	КонецЕсли;

	Возврат Состояние;

КонецФункции // ВызватьФункцию()


Функция Интерпретировать(Знач Узел, ЭтоАтрибут = Ложь, НачальныйУзел = Истина) Экспорт
	Перем Имя, Значение, Результат;

	Если НЕ ТипЗнч(Узел) = Тип("Структура") Тогда
		ВызватьИсключение "Неверный узел: " + Узел;
		//Сообщить("Это не узел");
		Возврат "";
	КонецЕсли;

	СледУзел = Узел;
	Пока НЕ СледУзел = Неопределено Цикл
		Узел = СледУзел;

		Состояние = Неопределено;

		Имя = Узел.Имя;
		Значение = УзелСвойство(Узел, "Значение");

		//Сообщить("у " + Узел.Код + " " + Имя + " " + Значение);

		Если Имя = "Истина" Тогда
			Состояние = Истина;
		ИначеЕсли Имя = "Ложь" Тогда
			Состояние = Ложь;
		ИначеЕсли Имя = "Неопределено" Тогда
			Состояние = Неопределено;
		ИначеЕсли Имя = "Пустой" Тогда
			Состояние = Пустой;
		ИначеЕсли Имя = "Число" Тогда
			Если НЕ "" + Значение = "" Тогда
				Состояние = Число(Значение);
			Иначе
				Если НЕ Узел.Дочерний = Неопределено Тогда
					Состояние = Число(Интерпретировать(Узел.Дочерний, , Ложь));
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Имя = "Пробел" Тогда
			Состояние = " ";
		ИначеЕсли Имя = "Источник" Тогда
			Состояние = Процесс.Источник;
		ИначеЕсли Имя = "Процесс" Тогда
			Состояние = Процесс.procid;
		ИначеЕсли Имя = "Строка" И НЕ ЭтоАтрибут Тогда
			Состояние = "" + Значение;
			Если НЕ Узел.Дочерний = Неопределено Тогда
				Состояние = Состояние + Интерпретировать(Узел.Дочерний, , Ложь);
			КонецЕсли;
		ИначеЕсли Имя = "Структура" Тогда
			Состояние = Значение;
		ИначеЕсли (Имя = "Аргумент" ИЛИ Имя = "А") И этоАтрибут Тогда
			// объявление аргумента
			Если Значение = "Значение" Тогда // текстовое поле
				Если НЕ Узел.Дочерний = Неопределено Тогда
					Состояние = " value=""" + Интерпретировать(Узел.Дочерний) + """ onchange=""addcmd(this,event)""";
				КонецЕсли;
			ИначеЕсли Значение = "Выбрано" Тогда // галочка
				Если НЕ Узел.Дочерний = Неопределено Тогда
					Состояние = ?(Интерпретировать(Узел.Дочерний) = "true", " checked=""checked""", "");
				КонецЕсли;
			ИначеЕсли Значение = "ПриИзменении" Тогда
				Состояние = " onchange=""addcmd(this,event)""";
			ИначеЕсли Значение = "ПриНажатии" Тогда
				Состояние = " onclick=""addcmd(this,event); return false""";
			ИначеЕсли Значение = "ПриОтправке" Тогда
				Состояние = " onsubmit=""addcmd(this,event); return false""";
			КонецЕсли;

		ИначеЕсли Имя = "Функция" ИЛИ Имя = "Ф" Тогда
			Если Узел.Дочерний = Неопределено Тогда
				Состояние = "" + ВызватьФункцию(Узел);
			КонецЕсли;
			Если НЕ Узел.Дочерний = Неопределено Тогда
				Состояние = Состояние + Интерпретировать(Узел.Дочерний, , Ложь);
			КонецЕсли;

		ИначеЕсли Имя = "Свойства." Тогда // свойства объекта

		ИначеЕсли Имя = "Объект" ИЛИ Имя = "О" Тогда // это функциональный узел
			Состояние = "" + ОбработатьОбъект(Узел);

		ИначеЕсли Имя = "" Тогда // это оператор
			Параметры = Новый Массив;
			Параметры.Добавить(ЭтотОбъект);
			Параметры.Добавить(Узел.Дочерний);
			ИмяФункции = Значение;
			Если НЕ "" + Значение = "" Тогда
				Если СтрНайти(Значение, ".") Тогда
					ИмяФункции = СтрРазделить(ИмяФункции, ".");
					ИмяБиблиотеки = ИмяФункции[0];
					ИмяФункции = ИмяФункции[1];
				Иначе
					ИмяБиблиотеки = "Операторы";
				КонецЕсли;
				Библиотека = Процесс.ПолучитьБиблиотеку(ИмяБиблиотеки);
				Состояние = Рефлектор.ВызватьМетод(Библиотека, "Оператор_" + ИмяФункции, Параметры);
			КонецЕсли;

		ИначеЕсли Имя = "Ссылка" ИЛИ Имя = "С" Тогда
			Свойства = ПолучитьСвойстваПоСсылке(Узел, , Истина);
			Если Свойства.Количество() = 0 Тогда
				ВызватьИсключение "Свойство не найдено: " + Узел.Значение;
			КонецЕсли;
			Состояние = Свойства[0];

		ИначеЕсли Имя = "Переменная" ИЛИ Имя = "П" Тогда // значение переменной объекта
			сУзел = Узел;
			Пока НЕ сУзел = Неопределено Цикл
				Если сУзел.Имя = "Свойства." Тогда
					Состояние = "" + УзелСвойство(сУзел.Родитель, Значение);
					Прервать;
				КонецЕсли;
				сУзел = сУзел.Родитель;
			КонецЦикла;

		ИначеЕсли Имя = "Значение" ИЛИ Имя = "З" Тогда // значение свойств объектов
			Состояние = "";
			сУзел = Узел;
			Свойства = ПолучитьСвойстваПоСсылке(Узел, Значение, Истина);
			Если Свойства.Количество() = 0 Тогда
				Сообщить("Свойство не найдено: " + Значение + " узел " + Узел.Код);
			КонецЕсли;
			Для каждого Свойство Из Свойства Цикл
				Если Состояние = "" Тогда
					Состояние = ЗначениеСвойства(Свойство);
				Иначе
					Состояние = "" + Состояние + " " + ЗначениеСвойства(Свойство);
				КонецЕсли;
			КонецЦикла;

		ИначеЕсли Имя = "Свойство" Тогда
			Элемент = Интерпретировать(Узел.Дочерний);
			Состояние = УзелСвойство(Элемент, Значение);
		ИначеЕсли Имя = "Атрибут" Тогда
			Элемент = Интерпретировать(Узел.Дочерний);
			Состояние = НайтиСоседний(Элемент.Атрибут, Значение).Значение;
		ИначеЕсли Имя = "Первый" Тогда
			Список = Интерпретировать(Узел.Дочерний);
			Элемент = Список.Дочерний;
			Если Элемент = Неопределено Тогда
				Элемент = Пустой;
			КонецЕсли;
			Состояние = Элемент;
		ИначеЕсли Имя = "Соседний" Тогда
			Элемент = Интерпретировать(Узел.Дочерний);
			Если НЕ Элемент = Неопределено Тогда
				Элемент = Элемент.Соседний;
			КонецЕсли;
			Если Элемент = Неопределено Тогда
				Элемент = Пустой;
			КонецЕсли;
			Состояние = Элемент;
		Иначе

			Если НЕ ЭтоАтрибут Тогда
				ЗначениеУзелДочерний = "";
				Если НЕ Узел.Дочерний = Неопределено Тогда
					ЗначениеУзелДочерний = Интерпретировать(Узел.Дочерний, , Ложь);
				КонецЕсли;
				ЗначениеУзелАтрибут = "";
				Если НЕ Узел.Атрибут = Неопределено Тогда
					ЗначениеУзелАтрибут = Интерпретировать(Узел.Атрибут, Истина, Ложь);
				КонецЕсли;
				Состояние = ПоказатьУзел(Узел, ЗначениеУзелАтрибут, ЗначениеУзелДочерний);
			Иначе
				ЗначениеУзелДочерний = "";
				Если НЕ Узел.Дочерний = Неопределено Тогда
					ЗначениеУзелДочерний = Интерпретировать(Узел.Дочерний, , Ложь);
				КонецЕсли;
				Состояние = ПоказатьУзел(Узел, , ЗначениеУзелДочерний, Истина);
			КонецЕсли;

		КонецЕсли;

		Если НачальныйУзел Тогда
			Возврат Состояние;
		КонецЕсли;

		К = К + 1;
		Если Результат = Неопределено Тогда
			Результат = Состояние;
		Иначе
			Результат = Результат + Состояние;
		КонецЕсли;

		СледУзел = Узел.Соседний;

	КонецЦикла;

	Возврат Результат;

КонецФункции // Интерпретировать()


Функция НайтиПоКоду(Код, Старший) Экспорт
	Найден = Неопределено;

	Атрибут = Старший.Атрибут;
	Если НЕ Атрибут = Неопределено Тогда
		Если Атрибут.Код = Код Тогда
			Найден = Атрибут;
		КонецЕсли;
	КонецЕсли;
	Если Найден = Неопределено Тогда
		Дочерний = Старший.Дочерний;
		Если НЕ Дочерний = Неопределено Тогда
			Если Дочерний.Код = Код Тогда
				Найден = Дочерний;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если Найден = Неопределено Тогда
		Соседний = Старший.Соседний;
		Если НЕ Соседний = Неопределено Тогда
			Если Соседний.Код = Код Тогда
				Найден = Соседний;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если Найден = Неопределено Тогда
		Если НЕ Атрибут = Неопределено Тогда
			Найден = НайтиПоКоду(Код, Атрибут);
		КонецЕсли;
	КонецЕсли;
	Если Найден = Неопределено Тогда
		Если НЕ Дочерний = Неопределено Тогда
			Найден = НайтиПоКоду(Код, Дочерний);
		КонецЕсли;
	КонецЕсли;
	Если Найден = Неопределено Тогда
		Если НЕ Соседний = Неопределено Тогда
			Найден = НайтиПоКоду(Код, Соседний);
		КонецЕсли;
	КонецЕсли;

	Возврат Найден;

КонецФункции


Функция ПолучитьУзел(Код, Старший = Неопределено) Экспорт
	Узел = Узлы.Получить(Код);
	Если НЕ Узел = Неопределено Тогда
		Возврат Узел;
	КонецЕсли;
	// Если Лев(Код, 1) = "s" Тогда
	// 	Возврат Неопределено
	// КонецЕсли;
	//Стр = Данные.ПолучитьСтроку(Число(Код));
	Попытка
		Стр = Данные.ПолучитьСтроку(Число(Код));
	Исключение
		Возврат Неопределено;
		//ВызватьИсключение "Неверный код узла: " + Код;
	КонецПопытки;
	Стр = СтрРазделить(Стр, Символы.Таб);
	Ключ = Неопределено;
	Для Каждого знСтр Из Стр Цикл
		Если Ключ = Неопределено Тогда
			Ключ = знСтр;
		Иначе
			Если Узел = Неопределено Тогда
				Узел = Новый Структура("Код, Имя, Дочерний, Соседний, Атрибут", Код, "");
			КонецЕсли;
			Если Ключ = "И" Тогда
				Ключ = "Имя";
			ИначеЕсли Ключ = "З" Тогда
				Ключ = "Значение";
				знСтр = СтрЗаменить(знСтр, "#x9", Символы.Таб);
				знСтр = СтрЗаменить(знСтр, "#xA", Символы.ПС);
				знСтр = СтрЗаменить(знСтр, "#xD", Символы.ВК);
			ИначеЕсли Ключ = "Д" Тогда
				Ключ = "Дочерний";
			ИначеЕсли Ключ = "С" Тогда
				Ключ = "Соседний";
			ИначеЕсли Ключ = "А" Тогда
				Ключ = "Атрибут";
			КонецЕсли;
			Узел.Вставить(Ключ, знСтр);
			Ключ = Неопределено;
		КонецЕсли;
	КонецЦикла;

	Если НЕ Узел = Неопределено Тогда

		Если НЕ Старший = Неопределено Тогда
			Узел.Вставить("Старший", Старший);
			Родитель = Старший;
			Если Старший.Соседний = Узел.Код Тогда
				Родитель = Старший.Родитель;
			КонецЕсли;
			Узел.Вставить("Родитель", Родитель);
			Если УзелСвойство(Родитель, "Атрибут") = Узел.Код ИЛИ (Старший.Свойство("ЭтоАтрибут") = Истина И Старший.Соседний = Узел.Код) Тогда
				Узел.Вставить("ЭтоАтрибут", Истина);
			КонецЕсли;
		КонецЕсли;

		УзелИмя = Узел.Имя;
		Если НЕ УзелИмя = "" Тогда
			Если Прав(УзелИмя, 1) = "." Тогда
				Узел.Вставить("д", Новый Структура);
				УзелИмя = Лев(УзелИмя, СтрДлина(УзелИмя)-1);
			КонецЕсли;
			Родитель = УзелСвойство(Узел, "Родитель");
			Если НЕ Родитель = Неопределено Тогда
				Если Узел.Родитель.Свойство("д") Тогда
					Узел.Родитель.д.Вставить(УзелИмя, Узел);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Узлы.Вставить(Код, Узел);

		Если НЕ Узел.Дочерний = Неопределено Тогда
			Узел.Дочерний = ПолучитьУзел(Узел.Дочерний, Узел);
		КонецЕсли;
		Если НЕ Узел.Соседний = Неопределено Тогда
			Узел.Соседний = ПолучитьУзел(Узел.Соседний, Узел);
		КонецЕсли;
		Если НЕ Узел.Атрибут = Неопределено Тогда
			Узел.Атрибут = ПолучитьУзел(Узел.Атрибут, Узел);
		КонецЕсли;

	КонецЕсли;

	Возврат Узел;
КонецФункции // ПолучитьУзел()

Функция НовыйУзел(Узел, Служебный = Ложь) Экспорт
	Если Служебный Тогда
		НовыйКод = "s" + сКоличество;
		сКоличество = сКоличество + 1;
	Иначе
		Пока КодУзла <= Количество Цикл
			КодУзла = КодУзла + 1;
			Если КодУзла > Количество Тогда
				Данные.ДобавитьСтроку("");
				Количество = Данные.КоличествоСтрок();
				Прервать;
			КонецЕсли;
			Если Данные.ПолучитьСтроку(КодУзла) = "" Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		НовыйКод = "" + КодУзла;
	КонецЕсли;

	УзелИмя = Узел.Имя;
	Если НЕ УзелИмя = "" Тогда
		Если Прав(УзелИмя, 1) = "." Тогда
			Узел.Вставить("д", Новый Структура);
			УзелИмя = Лев(УзелИмя, СтрДлина(УзелИмя)-1);
		КонецЕсли;
		Родитель = УзелСвойство(Узел, "Родитель");
		Если НЕ Родитель = Неопределено Тогда
			Если Узел.Родитель.Свойство("д") Тогда
				Узел.Родитель.д.Вставить(УзелИмя, Узел);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если НЕ Узел.Свойство("Дочерний") Тогда
		Узел.Вставить("Дочерний");
	КонецЕсли;
	Если НЕ Узел.Свойство("Соседний") Тогда
		Узел.Вставить("Соседний");
	КонецЕсли;
	Если НЕ Узел.Свойство("Атрибут") Тогда
		Узел.Вставить("Атрибут");
	КонецЕсли;

	Узел.Вставить("Код", НовыйКод);
	Узлы.Вставить(Узел.Код, Узел);
	Возврат Узел;
КонецФункции // НовыйУзел(СтруктураУзла)

Функция НовыйРодитель(Дочерний, СтруктураУзла, Служебный = Ложь) Экспорт
	СтруктураУзла.Вставить("Старший", Дочерний.Старший);
	СтруктураУзла.Вставить("Родитель", Дочерний.Родитель);
	СтруктураУзла.Вставить("Дочерний", Дочерний);
	НовыйУзел = НовыйУзел(СтруктураУзла, Служебный);
	СтаршийУзел = Дочерний.Старший;
	Если СтаршийУзел.Дочерний = Дочерний Тогда
		СтаршийУзел.Дочерний = НовыйУзел;
	КонецЕсли;
	Если СтаршийУзел.Соседний = Дочерний Тогда
		СтаршийУзел.Соседний = НовыйУзел;
	КонецЕсли;
	СоседнийУзел = Дочерний.Соседний;
	Если НЕ СоседнийУзел = Неопределено Тогда
		СоседнийУзел.Старший = НовыйУзел;
		НовыйУзел.Вставить("Соседний", СоседнийУзел);
	КонецЕсли;
	Дочерний.Вставить("Соседний", Неопределено);
	Дочерний.Вставить("Старший", НовыйУзел);
	Дочерний.Вставить("Родитель", НовыйУзел);
	Возврат НовыйУзел;
КонецФункции // НовыйРодитель()

Функция УдалитьРодителя(Дочерний) Экспорт
	РодительУзел = Дочерний.Родитель;
	СтаршийУзел = РодительУзел.Старший;
	Если СтаршийУзел.Дочерний = РодительУзел Тогда
		СтаршийУзел.Дочерний = Дочерний;
	КонецЕсли;
	Если СтаршийУзел.Соседний = РодительУзел Тогда
		СтаршийУзел.Соседний = Дочерний;
	КонецЕсли;
	Дочерний.Вставить("Старший", РодительУзел.Старший);
	Дочерний.Вставить("Родитель", РодительУзел.Родитель);
	СоседнийУзел = Дочерний;
	Пока НЕ СоседнийУзел.Соседний = Неопределено Цикл
		СоседнийУзел = СоседнийУзел.Соседний;
	КонецЦикла;
	СоседнийУзелРодитель = РодительУзел.Соседний;
	Если НЕ СоседнийУзелРодитель = Неопределено Тогда
		СоседнийУзел.Вставить("Соседний", СоседнийУзелРодитель);
		СоседнийУзелРодитель.Старший = СоседнийУзел;
	КонецЕсли;
КонецФункции // УдалитьРодителя()

Функция НовоеЗначениеУзла(Знач Узел, Значение = Неопределено, Служебный = Ложь, Добавить = Ложь, Обновить = Истина) Экспорт
	ДочернийУзел = Узел.Дочерний;
	Если НЕ ДочернийУзел = Неопределено Тогда
		Если НЕ Добавить Тогда
			// прежний дочерний нужно удалять
			УдалитьУзел(ДочернийУзел, , Истина);
			ДочернийУзел = Неопределено;
		КонецЕсли;
	КонецЕсли;
	Если Значение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если НЕ ТипЗнч(Значение) = Тип("Структура") ИЛИ НЕ Значение.Свойство("Имя") Тогда
		Значение = ИмяЗначение(Строка(ТипЗнч(Значение)), Значение);
	КонецЕсли;
	нУзел = НовыйДочерний(Узел, Значение, Служебный, НЕ ДочернийУзел = Неопределено);
	Если Обновить Тогда
		ОбновитьУзел(Узел);
	КонецЕсли;
	Возврат нУзел;
КонецФункции // НовоеЗначениеУзла()

Функция НовыйДочерний(Знач Старший, СтруктураУзла, Служебный = Ложь, ВКонец = Ложь) Экспорт
	Если ВКонец Тогда
		сУзел = Старший.Дочерний;
		Если НЕ сУзел = Неопределено Тогда
			Пока НЕ сУзел = Неопределено Цикл
				Старший = сУзел;
				сУзел = сУзел.Соседний;
			КонецЦикла;
			Возврат НовыйСоседний(Старший, СтруктураУзла, Служебный);
		КонецЕсли;
	КонецЕсли;
	СтруктураУзла.Вставить("Старший", Старший);
	СтруктураУзла.Вставить("Родитель", Старший);
	УзелСоседний = Старший.Дочерний;
	Если НЕ УзелСоседний = Неопределено Тогда
		СтруктураУзла.Вставить("Соседний", УзелСоседний);
	КонецЕсли;
	Если СтруктураУзла.Свойство("Код") Тогда // существующий узел
		НовыйУзел = СтруктураУзла;
	Иначе
		НовыйУзел = НовыйУзел(СтруктураУзла, Служебный);
	КонецЕсли;
	Старший.Вставить("Дочерний", НовыйУзел);
	Если НЕ УзелСоседний = Неопределено Тогда
		УзелСоседний.Вставить("Старший", НовыйУзел);
	КонецЕсли;
	// Если УзелСостояние(Старший, "ЭтоАтрибут") = Истина Тогда
	// 	УзелСостояниеЗначение(НовыйУзел, "ЭтоАтрибут", Истина);
	// КонецЕсли;
	Возврат НовыйУзел;
КонецФункции // НовыйДочерний()

Функция НовыйСоседний(Старший, СтруктураУзла, Служебный = Ложь) Экспорт
	СтруктураУзла.Вставить("Старший", Старший);
	СтруктураУзла.Вставить("Родитель", Старший.Родитель);
	УзелСоседний = Старший.Соседний;
	Если НЕ УзелСоседний = Неопределено Тогда
		СтруктураУзла.Вставить("Соседний", УзелСоседний);
	КонецЕсли;
	НовыйУзел = НовыйУзел(СтруктураУзла, Служебный);
	Старший.Вставить("Соседний", НовыйУзел);
	Если НЕ УзелСоседний = Неопределено Тогда
		УзелСоседний.Вставить("Старший", НовыйУзел);
	КонецЕсли;
	Если Старший.Свойство("ЭтоАтрибут") = Истина Тогда
		НовыйУзел.Вставить("ЭтоАтрибут", Истина);
	КонецЕсли;
	Возврат НовыйУзел;
КонецФункции // НовыйСоседний()

Функция НовыйПоследний(Знач Старший, СтруктураУзла, Служебный = Ложь) Экспорт
	УзелСоседний = Старший.Соседний;
	Пока НЕ УзелСоседний = Неопределено Цикл
		Старший = УзелСоседний;
		УзелСоседний = Старший.Соседний;
	КонецЦикла;
	СтруктураУзла.Вставить("Старший", Старший);
	СтруктураУзла.Вставить("Родитель", Старший.Родитель);
	НовыйУзел = НовыйУзел(СтруктураУзла, Служебный);
	Старший.Вставить("Соседний", НовыйУзел);
	Если Старший.Свойство("ЭтоАтрибут") = Истина Тогда
		НовыйУзел.Вставить("ЭтоАтрибут", Истина);
	КонецЕсли;
	Возврат НовыйУзел;
КонецФункции // НовыйПоследний()

Функция НовыйАтрибут(Старший, СтруктураУзла, Служебный = Ложь) Экспорт
	СтруктураУзла.Вставить("Старший", Старший);
	СтруктураУзла.Вставить("Родитель", Старший);
	УзелСоседний = Старший.Атрибут;
	Если НЕ УзелСоседний = Неопределено Тогда
		СтруктураУзла.Вставить("Соседний", УзелСоседний);
	КонецЕсли;
	НовыйУзел = НовыйУзел(СтруктураУзла, Служебный);
	Старший.Вставить("Атрибут", НовыйУзел);
	Если НЕ УзелСоседний = Неопределено Тогда
		УзелСоседний.Вставить("Старший", НовыйУзел);
	КонецЕсли;
	НовыйУзел.Вставить("ЭтоАтрибут", Истина);
	Возврат НовыйУзел;
КонецФункции // НовыйАтрибут()

Функция Служебный(Узел)
	Если НЕ Узел = Неопределено Тогда
		Если Лев(Узел.Код, 1) = "s" Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат Ложь;
КонецФункции // Служебный()

Функция УдалитьУзел(Узел, Совсем = Истина, Цепочку = Ложь, НачальныйУзел = Истина) Экспорт

	УзелСоседний = Узел.Соседний;

	Если НачальныйУзел Тогда
		Если Узел.Родитель.Свойство("д") Тогда // удалить из индекса
			УзелИмя = Узел.Имя;
			Если НЕ УзелИмя = "" Тогда
				Если Прав(УзелИмя, 1) = "." Тогда
					УзелИмя = Лев(УзелИмя, СтрДлина(УзелИмя)-1);
				КонецЕсли;
				Узел.Родитель.д.Удалить(УзелИмя);
			КонецЕсли;
		КонецЕсли;
		УзелСтарший = Узел.Старший;
		Если УзелСтарший.Атрибут = Узел Тогда
			Если УзелСоседний = Неопределено ИЛИ Цепочку Тогда
				//УзелСтарший.Удалить("Атрибут");
				УзелСтарший.Атрибут = Неопределено;
			Иначе
				УзелСтарший.Атрибут = УзелСоседний;
				УзелСоседний.Старший = УзелСтарший;
			КонецЕсли
		КонецЕсли;
		Если УзелСтарший.Дочерний = Узел Тогда
			Если УзелСоседний = Неопределено ИЛИ Цепочку Тогда
				//УзелСтарший.Удалить("Дочерний");
				УзелСтарший.Дочерний = Неопределено;
			Иначе
				УзелСтарший.Дочерний = УзелСоседний;
				УзелСоседний.Старший = УзелСтарший;
			КонецЕсли
		КонецЕсли;
		Если УзелСтарший.Соседний = Узел Тогда
			Если УзелСоседний = Неопределено ИЛИ Цепочку Тогда
				//УзелСтарший.Удалить("Соседний");
				УзелСтарший.Соседний = Неопределено;
			Иначе
				УзелСтарший.Соседний = УзелСоседний;
				УзелСоседний.Старший = УзелСтарший;
			КонецЕсли
		КонецЕсли;
	КонецЕсли;

	Если Цепочку Тогда
		Если НЕ УзелСоседний = Неопределено Тогда
			УдалитьУзел(УзелСоседний, Совсем, Истина, Ложь);
		КонецЕсли;
	КонецЕсли;

	// удалить связи
	Для каждого элСвязь Из ВсеСвязи Цикл
		Связи = элСвязь.Значение;
		уСвязи = Новый Соответствие;
		уСвязи.Вставить(Узел);
		Для каждого элУзел Из Связи Цикл
			Если элУзел.Значение = Узел Тогда
				уСвязи.Вставить(элУзел.Ключ);
			КонецЕсли;
		КонецЦикла;
		Для каждого элУзел Из уСвязи Цикл
			Связи.Удалить(элУзел.Ключ);
		КонецЦикла;
	КонецЦикла;
	ВсеСвязи.Удалить(Узел);

	Узлы.Удалить(Узел.Код);
	Сообщить("Узел удален " + Узел.Код);
	ЭтоСлужебный = Служебный(Узел);
	Если НЕ ЭтоСлужебный Тогда
		Данные.ЗаменитьСтроку(Узел.Код, "");
	КонецЕсли;

	Если Совсем ИЛИ ЭтоСлужебный Тогда
		Если НЕ Узел.Атрибут = Неопределено Тогда
			УдалитьУзел(Узел.Атрибут, , Истина, Ложь);
		КонецЕсли;
		Если НЕ Узел.Дочерний = Неопределено Тогда
			УдалитьУзел(Узел.Дочерний, , Истина,  Ложь);
		КонецЕсли;
		ОсвободитьОбъект(Узел);
	КонецЕсли;

КонецФункции // УдалитьУзел(Узел)

Функция КопироватьУзел(Узел, Буфер, ПервыйВызов = Истина) Экспорт

	Буфер.Вставить(Узел.Код, Узел);

	Если НЕ Узел.Атрибут = Неопределено Тогда
		КопироватьУзел(Узел.Атрибут, Буфер, Ложь);
	КонецЕсли;

	Если НЕ Узел.Дочерний = Неопределено Тогда
		КопироватьУзел(Узел.Дочерний, Буфер, Ложь);
	КонецЕсли;

	Если НЕ ПервыйВызов Тогда
		Если НЕ Узел.Соседний = Неопределено Тогда
			КопироватьУзел(Узел.Соседний, Буфер, Ложь);
		КонецЕсли;
	КонецЕсли;

КонецФункции // КопироватьУзел()

Функция НайтиСоседний(Знач Узел, ИмяУзла) Экспорт
	Пока НЕ Узел = Неопределено Цикл
		Если Узел.Имя = ИмяУзла Тогда
			Возврат Узел;
		КонецЕсли;
		Узел = Узел.Соседний;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции // НайтиСоседний()

Функция НайтиАтрибут(Знач Узел, ИмяАтрибута, ЗначениеАтрибута = Неопределено) Экспорт
	Узел = Узел.Атрибут;
	Пока НЕ Узел = Неопределено Цикл
		Если Узел.Имя = ИмяАтрибута Тогда
			Если ЗначениеАтрибута = Неопределено Тогда
				Возврат Узел;
			ИначеЕсли ЗначениеАтрибута = УзелСвойство(Узел, "Значение") Тогда
				Возврат Узел;
			КонецЕсли;
		КонецЕсли;
		Узел = Узел.Соседний;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции // НайтиАтрибут()

// Функция Соседний(Знач Узел) Экспорт
// 	Если НЕ ТипЗнч(Узел) = Тип("Структура") Тогда
// 		ВызватьИсключение "Неверный узел";
// 	КонецЕсли;
// 	СоседнийУзел = УзелСвойство(Узел, "Соседний");
// 	Если НЕ СоседнийУзел = Неопределено Тогда
// 		Если НЕ ТипЗнч(СоседнийУзел) = Тип("Структура") Тогда
// 			СоседнийУзел = ПолучитьУзел(СоседнийУзел, Узел);
// 		КонецЕсли;
// 		Если Узел.Свойство("ЭтоАтрибут") = Истина Тогда
// 			СоседнийУзел.Вставить("ЭтоАтрибут", Истина);
// 		КонецЕсли;
// 	КонецЕсли;
// 	Возврат СоседнийУзел;
// КонецФункции // Соседний()
//
// Функция Дочерний(Знач Узел) Экспорт
// 	Если НЕ ТипЗнч(Узел) = Тип("Структура") Тогда
// 		ВызватьИсключение "Неверный узел";
// 	КонецЕсли;
// 	// ДочернийУзел = УзелСвойство(Узел, "сДочерний"); // Если изменено значение узла
// 	// Если ДочернийУзел = Неопределено Тогда
// 		ДочернийУзел = УзелСвойство(Узел, "Дочерний");
// 	//КонецЕсли;
// 	Если НЕ ДочернийУзел = Неопределено Тогда
// 		Если НЕ ТипЗнч(ДочернийУзел) = Тип("Структура") Тогда
// 			ДочернийУзел = ПолучитьУзел(ДочернийУзел, Узел);
// 		КонецЕсли;
// 	КонецЕсли;
// 	Возврат ДочернийУзел;
// КонецФункции // Дочерний()
//
// Функция Атрибут(Знач Узел) Экспорт
// 	Если НЕ ТипЗнч(Узел) = Тип("Структура") Тогда
// 		ВызватьИсключение "Неверный узел";
// 	КонецЕсли;
// 	АтрибутУзел = УзелСвойство(Узел, "Атрибут");
// 	Если НЕ АтрибутУзел = Неопределено Тогда
// 		Если НЕ ТипЗнч(АтрибутУзел) = Тип("Структура") Тогда
// 			АтрибутУзел = ПолучитьУзел(АтрибутУзел, Узел);
// 		КонецЕсли;
// 	КонецЕсли;
// 	Возврат АтрибутУзел;
// КонецФункции // Атрибут()

Функция СохранитьДанные() Экспорт
	Для каждого элУзел Из Узлы Цикл
		Если НЕ элУзел.Значение = Неопределено И НЕ Служебный(элУзел.Значение) Тогда
			Данные.ЗаменитьСтроку(ЭлУзел.Ключ, СтруктуруВСтроку(элУзел.Значение));
		КонецЕсли;
	КонецЦикла;
	Возврат Данные.ПолучитьТекст();
КонецФункции // СохранитьДанные()

Функция ПрочитатьВетку(Знач Узел)
	Если НЕ Узел = Неопределено Тогда
		ПрочитатьВетку(Узел.Атрибут);
		ПрочитатьВетку(Узел.Дочерний);
		ПрочитатьВетку(Узел.Соседний);
	КонецЕсли;
КонецФункции

Функция ПроверитьДанные()
	ПрочитатьВетку(Корень);
	Для нСтр = 1 По Данные.КоличествоСтрок() Цикл
		Если Узлы.Получить(Строка(нСтр)) = Неопределено Тогда
			Стр = Данные.ПолучитьСтроку(нСтр);
			Если НЕ Стр = "" Тогда
				Сообщить("Забытая строка " + нСтр);
				Данные.ЗаменитьСтроку(нСтр, "");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецФункции


// поиск узла внутри узла
Функция НайтиУзел(Знач Узел, ЗначениеУзла, ИмяУзла = "", ПервыйВызов = Истина) Экспорт
	Объявление = Неопределено;
	Если Узел.Имя = ИмяУзла ИЛИ (ИмяУзла = "" И (Узел.Имя = "Узел" ИЛИ Узел.Имя = "У" ИЛИ Узел.Имя = "Объект" ИЛИ Узел.Имя = "О")) Тогда
		Если "" + УзелСвойство(Узел, "Значение") = ЗначениеУзла Тогда
			Возврат Узел;
		ИначеЕсли ПервыйВызов Тогда
			Если НЕ Узел.Дочерний = Неопределено Тогда
				Объявление = НайтиУзел(Узел.Дочерний, ЗначениеУзла, ИмяУзла, Ложь);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если НЕ Узел.Дочерний = Неопределено Тогда
			Объявление = НайтиУзел(Узел.Дочерний, ЗначениеУзла, ИмяУзла, Ложь);
		КонецЕсли;
	КонецЕсли;
	Если Объявление = Неопределено Тогда
		Если НЕ Узел.Соседний = Неопределено Тогда
			Объявление = НайтиУзел(Узел.Соседний, ЗначениеУзла, ИмяУзла, Ложь);
		КонецЕсли;
	КонецЕсли;
	Возврат Объявление;
КонецФункции // НайтиУзел()


Функция ЗагрузитьHTML(Знач Узел, СтруктураHTML, НомерЭлемента) Экспорт
	Пока НомерЭлемента < СтруктураHTML.Количество() - 1 Цикл
		УзелСтрока = СтруктураHTML.Получить(НомерЭлемента);
		Если УзелСтрока = "_rt" Тогда
			Прервать;
		ИначеЕсли УзелСтрока = "_at" Тогда
			НомерЭлемента = НомерЭлемента + 1;
			УзелСтрока = СтруктураHTML.Получить(НомерЭлемента);
			Пока НЕ УзелСтрока = "_rt" Цикл
				стрУзла = СтрокуВСтруктуру(УзелСтрока);
				НовыйАтрибут(Узел, ИмяЗначение(стрУзла.attrName, стрУзла.attrVal));
				НомерЭлемента = НомерЭлемента + 1;
				УзелСтрока = СтруктураHTML.Получить(НомерЭлемента);
			КонецЦикла;
			НомерЭлемента = НомерЭлемента + 1;
			Продолжить;
		ИначеЕсли УзелСтрока = "_ch" Тогда
			НомерЭлемента = НомерЭлемента + 1;
			дУзел = НовыйДочерний(Узел, ИмяЗначение());
			ЗагрузитьHTML(дУзел, СтруктураHTML, НомерЭлемента)
		Иначе
			Если НЕ Узел.Имя = "" Тогда
				Узел = НовыйСоседний(Узел, ИмяЗначение());
			КонецЕсли;
			стрУзла = СтрокуВСтруктуру(УзелСтрока);
			Если стрУзла.Свойство("tagName") Тогда
				Узел.Вставить("Имя", стрУзла.tagName);
			Иначе
				Узел.Вставить("Имя", "text");
				Узел.Вставить("Значение", стрУзла.text);
			КонецЕсли;
		КонецЕсли;
		НомерЭлемента = НомерЭлемента + 1;
	КонецЦикла;
КонецФункции // ЗагрузитьHTML()


Функция ПолучитьТекст() Экспорт
	Возврат Данные.ПолучитьТекст();
КонецФункции

Функция ПриСозданииОбъекта(обПроцесс, Текст = "", знБазаДанных = "", знИмяДанных = "", знПозицияДанных = "") Экспорт
	БазаДанных = знБазаДанных;
	ИмяДанных = знИмяДанных;
	ПозицияДанных = знПозицияДанных;
	Процесс = обПроцесс;

	Данные = Новый ТекстовыйДокумент;
	Узлы = Новый Соответствие;
	Если НЕ Текст = "" Тогда
		Данные.УстановитьТекст(Текст);
		Стр1 = Данные.ПолучитьСтроку(1);
		Если КодСимвола(Лев(Стр1, 1)) = 65279 Тогда // BOM
			Данные.ЗаменитьСтроку(1, Сред(Стр1, 2));
		КонецЕсли;
	КонецЕсли;

	Количество = Данные.КоличествоСтрок();
	сКоличество = 0;
	КодУзла = 0;
	К = 0;

	Представление = "";
	Если Количество = 0 Тогда
		Корень = НовыйУзел(ИмяЗначение("О", "Корень"));
	Иначе
		Корень = ПолучитьУзел("1");
	КонецЕсли;
	Корень.Вставить("Родитель", Неопределено);
	Корень.Вставить("Старший", Неопределено);

	//ПроверитьДанные();

	Рефлектор = Новый Рефлектор;
	Пустой = НовыйУзел(Новый Структура("Имя, Родитель", "Пустой", Неопределено), Истина);
	НетЗначения = Новый Структура;
	УзлыОбновить = Новый Соответствие;
	ОбъектыОбновить = Новый Соответствие;
	ВсеСвязи = Новый Соответствие;

	Очередь = Новый Массив;
	Состояния = Новый Соответствие;

	//Изменены = Ложь;

КонецФункции
