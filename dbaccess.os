// /*----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------*/

Перем ИмяФайлаДанных;
Перем КаталогФайловДанных Экспорт;
Перем ПотокДанных;
Перем ВремяИзменения Экспорт;


Функция СтруктуруВДвоичныеДанные(знСтруктура)
	Результат = Новый Массив;
	Если НЕ знСтруктура = Неопределено Тогда
		Для каждого Элемент Из знСтруктура Цикл
			Ключ = Элемент.Ключ;
			Значение = Элемент.Значение;
			Если ТипЗнч(Значение) = Тип("Структура") Тогда
				Ключ = "*" + Ключ;
				дЗначение = СтруктуруВДвоичныеДанные(Значение);
			ИначеЕсли ТипЗнч(Значение) = Тип("ДвоичныеДанные") Тогда
				Ключ = "#" + Ключ;
				дЗначение = Значение;
			Иначе
				дЗначение = ПолучитьДвоичныеДанныеИзСтроки(Значение);
			КонецЕсли;
			дКлюч = ПолучитьДвоичныеДанныеИзСтроки(Ключ);
			рдКлюч = дКлюч.Размер();
			рдЗначение = дЗначение.Размер();
			бРезультат = Новый БуферДвоичныхДанных(6);
			бРезультат.ЗаписатьЦелое16(0, рдКлюч);
			бРезультат.ЗаписатьЦелое32(2, рдЗначение);
			Результат.Добавить(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бРезультат));
			Результат.Добавить(дКлюч);
			Результат.Добавить(дЗначение);
		КонецЦикла;
	КонецЕсли;
	Возврат СоединитьДвоичныеДанные(Результат);
КонецФункции


Функция ДвоичныеДанныеВСтруктуру(Данные, Рекурсия = Истина)
	Если ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
		рдДанные = Данные.Размер();
		Если рдДанные = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		бдДанные = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(Данные);
	ИначеЕсли ТипЗнч(Данные) = Тип("БуферДвоичныхДанных") Тогда
		рдДанные = Данные.Размер;
		бдДанные = Данные;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	Позиция = 0;
	знСтруктура = Новый Структура;
	Пока Позиция < рдДанные - 1 Цикл
		рдКлюч = бдДанные.ПрочитатьЦелое16(Позиция);
		рдЗначение = бдДанные.ПрочитатьЦелое32(Позиция + 2);
		Если рдКлюч + рдЗначение > рдДанные Тогда // Это не структура
			Возврат Неопределено;
		КонецЕсли;
		Ключ = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бдДанные.Прочитать(Позиция + 6, рдКлюч)));
		бЗначение = бдДанные.Прочитать(Позиция + 6 + рдКлюч, рдЗначение);
		Позиция = Позиция + 6 + рдКлюч + рдЗначение;
		Л = Лев(Ключ, 1);
		Если Л = "*" Тогда
			Если НЕ Рекурсия Тогда
				Продолжить;
			КонецЕсли;
			Ключ = Сред(Ключ, 2);
			Значение = ДвоичныеДанныеВСтруктуру(бЗначение);
		ИначеЕсли Л = "#" Тогда
			Ключ = Сред(Ключ, 2);
			Значение = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бЗначение);
		Иначе
			Значение = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бЗначение));
		КонецЕсли;
		знСтруктура.Вставить(Ключ, Значение);
	КонецЦикла;
	Возврат знСтруктура;
КонецФункции


// открыть контейнер для чтения или записи
Функция ОткрытьПотокДанных(ДляЗаписи = Ложь, Позиция = Неопределено) Экспорт

	Попытка

		Если ДляЗаписи Тогда

			Если НЕ ПотокДанных = Неопределено Тогда
				Если НЕ ПотокДанных.ДоступнаЗапись Тогда
					ПотокДанных.Закрыть();
					ПотокДанных = Неопределено;
				КонецЕсли;
			КонецЕсли;

			Если ПотокДанных = Неопределено Тогда
				ПотокДанных = ФайловыеПотоки.ОткрытьДляЗаписи(ИмяФайлаДанных);
			КонецЕсли;

			Если Позиция = Неопределено Тогда
				ПотокДанных.Перейти(0, ПозицияВПотоке.Конец);
			Иначе
				ПотокДанных.Перейти(Позиция, ПозицияВПотоке.Начало);
			КонецЕсли;

			ВремяИзменения = ТекущаяДата();

		Иначе

			Если НЕ ПотокДанных = Неопределено Тогда
				Если НЕ ПотокДанных.ДоступноЧтение Тогда
					ПотокДанных.Закрыть();
					ПотокДанных = Неопределено;
				КонецЕсли;
			КонецЕсли;

			Если ПотокДанных = Неопределено Тогда
				ПотокДанных = ФайловыеПотоки.ОткрытьДляЧтения(ИмяФайлаДанных);
			КонецЕсли;

			Если НЕ Позиция = Неопределено Тогда
				ПотокДанных.Перейти(Позиция, ПозицияВПотоке.Начало);
			КонецЕсли;

		КонецЕсли;

		Возврат Истина;

	Исключение

		Возврат Ложь;

	КонецПопытки;

КонецФункции


// Получить заголовки из контейнера
Функция ПолучитьЗаголовки() Экспорт

	ОткрытьПотокДанных();

	Если ПотокДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Индекс = 0;
	Результат = Новый Структура();

	ПозицияДанных = ПотокДанных.Размер();

	Пока ПозицияДанных > 12 Цикл

		ПотокДанных.Перейти(ПозицияДанных - 12, ПозицияВПотоке.Начало);

		Буфер = Новый БуферДвоичныхДанных(8);
		ПотокДанных.Прочитать(Буфер, 0, 8);
		ПозицияДанных = Буфер.ПрочитатьЦелое64(0);

		Буфер = Новый БуферДвоичныхДанных(4);
		ПотокДанных.Прочитать(Буфер, 0, 4);
		ТипДанных = Буфер.ПрочитатьЦелое32(0);

		Если ТипДанных = 1 Тогда // Заголовок
			дЗаголовок = ПолучитьДанные(ПозицияДанных);
			Результат.Вставить("З" + Индекс, ДвоичныеДанныеВСтруктуру(дЗаголовок));
			Индекс = Индекс + 1;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции // ПолучитьЗаголовки()


// найти заголовок по условиям
Функция НайтиЗаголовок(ЗапросДанных) Экспорт

	Перем СвойствоЗначение, ПозицияДанных, УсловияОтбора, Позиция, ЧислоЗаписей, Направление, ТолькоОдин;

	ОткрытьПотокДанных();

	Если ПотокДанных = Неопределено Тогда
		Возврат "ОшибкаПотокаДанных";
	КонецЕсли;

	ЗапросДанных.Свойство("УсловияОтбора", УсловияОтбора);
	Если ТипЗнч(УсловияОтбора) = Тип("Структура") Тогда
		Если НЕ УсловияОтбора.Количество() Тогда
			УсловияОтбора = Неопределено;
		КонецЕсли;
	Иначе
		УсловияОтбора = Неопределено;
	КонецЕсли;

	ЗапросДанных.Свойство("ПозицияДанных", ПозицияДанных);
	ЗапросДанных.Свойство("Направление", Направление);

	Если "" + Направление = "" Тогда
		Направление = "Назад";
	КонецЕсли;

	Если ПозицияДанных = Неопределено Тогда
		ЗапросДанных.Свойство("НачальнаяПозиция", ПозицияДанных);
	КонецЕсли;

	Размер = ПотокДанных.Размер();

	Если НЕ "" + ПозицияДанных = "" Тогда
		ПозицияДанных = Число(ПозицияДанных);
	Иначе
		ПозицияДанных = Размер;
	КонецЕсли;

	Если НЕ ЗапросДанных.Свойство("ПоследняяПозиция", Позиция) Тогда
		Позиция = 0;
	КонецЕсли;
	Если НЕ ЗапросДанных.Свойство("ЧислоЗаписей", ЧислоЗаписей) Тогда
		ЧислоЗаписей = 1;
	КонецЕсли;

	Позиция = Число(Позиция);
	ЧислоЗаписей = Число(ЧислоЗаписей);

	ЗапросДанных.Вставить("ПоследняяПозиция", Позиция);
	ЗапросДанных.Вставить("ЗаголовокНайден", Ложь);
	ЗаголовокНайден = Ложь;

	ВремяНачало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ЗаписейПрочитано = 0;

	Результат = "ЗапросВыполняется";

	Запись = Неопределено;

	Пока Истина Цикл

		Завершен = НЕ ((ПозицияДанных > 12 И Направление = "Назад") ИЛИ (ПозицияДанных + 4 < Размер И Направление = "Вперед")) ИЛИ (Позиция = ЧислоЗаписей);

		Если Завершен Тогда
			Прервать;
		КонецЕсли;

		ПотокДанных.Перейти(ПозицияДанных - 12, ПозицияВПотоке.Начало);

		Буфер = Новый БуферДвоичныхДанных(8);
		ПотокДанных.Прочитать(Буфер, 0, 8);
		зПозицияДанных = Буфер.ПрочитатьЦелое64(0);

		Буфер = Новый БуферДвоичныхДанных(4);
		ПотокДанных.Прочитать(Буфер, 0, 4);
		ТипДанных = Буфер.ПрочитатьЦелое32(0);

		ЗаписейПрочитано = ЗаписейПрочитано + 1;

		Если Направление = "Назад" Тогда
			ПозицияДанных = зПозицияДанных;
		Иначе
			ПотокДанных.Перейти(ПозицияДанных, ПозицияВПотоке.Начало);
			Буфер = Новый БуферДвоичныхДанных(4);
			ПотокДанных.Прочитать(Буфер, 0, 4);
			Объем = Буфер.ПрочитатьЦелое32(0);
			ПозицияДанных = ПозицияДанных + 4 + Объем + 12;
		КонецЕсли;

		Если ТипДанных = 1 Тогда // Заголовок

			дЗаголовок = ПолучитьДанные(зПозицияДанных);
			Заголовок = ДвоичныеДанныеВСтруктуру(дЗаголовок);

			// возвращает первое совпадение
			Если НЕ УсловияОтбора = Неопределено Тогда
				ЗаголовокНайден = Ложь;
				Для каждого элУсловия Из УсловияОтбора Цикл
					Если элУсловия.Ключ = "ТолькоОдин" Тогда
						ТолькоОдин = элУсловия.Значение.Значение;
					ИначеЕсли Заголовок.Свойство(элУсловия.Ключ, СвойствоЗначение) Тогда
						Если элУсловия.Значение.Сравнение = "Равно" Тогда
							ЗаголовокНайден = (СвойствоЗначение = элУсловия.Значение.Значение);
						КонецЕсли;
						Если НЕ ЗаголовокНайден Тогда
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ЗаголовокНайден = Истина;
			КонецЕсли;

			Если ЗаголовокНайден Тогда
				Если Запись = Неопределено Тогда
					ЗапросДанных.Вставить("ЗаголовокНайден", Истина);
					Запись = ЗапросДанных;
				Иначе
					Запись.Вставить("Соседний", Новый Структура());
					Запись = Запись.Соседний;
				КонецЕсли;
				Позиция = Позиция + 1;
				Запись.Вставить("Заголовок", Заголовок);
				Запись.Вставить("Позиция", Позиция);
				Если ТолькоОдин = "Истина" Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;

		// превышение времени ожидания ответа
		Если ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяНачало > 50 Тогда
			Прервать;
		КонецЕсли;

	КонецЦикла;

	ЗапросДанных.Вставить("ВремяПоиска", ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяНачало);
	ЗапросДанных.Вставить("ЗаписейПрочитано", ЗаписейПрочитано);

	ЗапросДанных.Вставить("ПоследняяПозиция", Позиция);
	ЗапросДанных.Вставить("ПозицияДанных", ПозицияДанных);

	Если Завершен Тогда
		Результат = "ЗапросЗавершен";
		Если ЗапросДанных.Свойство("Обновление") Тогда
			Если ЗапросДанных.Обновление = "Авто" Тогда
				Результат = "ЗапросПриостановлен";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции


// найти заголовок по ключу
Функция НайтиКлюч(Ключ) Экспорт

	ОткрытьПотокДанных();

	Если ПотокДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	ДанныеНайти = ПолучитьДвоичныеДанныеИзСтроки(Ключ);
	ОбъемДанных = ДанныеНайти.Размер();
	БуферНайти = Новый БуферДвоичныхДанных(ОбъемДанных);

	ПозицияДанных = ПотокДанных.Размер();

	Результат = Новый Структура("Результат", "ЗаголовокНеНайден");
	//Сообщить(ДанныеНайти);

	Пока ПозицияДанных > 12 Цикл

		ПотокДанных.Перейти(ПозицияДанных - 12, ПозицияВПотоке.Начало);

		Буфер = Новый БуферДвоичныхДанных(8);
		ПотокДанных.Прочитать(Буфер, 0, 8);
		ПозицияДанных = Буфер.ПрочитатьЦелое64(0);

		Буфер = Новый БуферДвоичныхДанных(4);
		ПотокДанных.Прочитать(Буфер, 0, 4);
		ТипДанных = Буфер.ПрочитатьЦелое32(0);

		Если ТипДанных = 1 Тогда // Заголовок

			ПотокДанных.Перейти(ПозицияДанных + 4, ПозицияВПотоке.Начало);
			ПотокДанных.Прочитать(БуферНайти, 0, ОбъемДанных);

			Если ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(БуферНайти) = ДанныеНайти Тогда
				дЗаголовок = ПолучитьДанные(ПозицияДанных);
				Результат.Вставить("Результат", "ЗаголовокНайден");
				Результат.Вставить("Заголовок", дЗаголовок);
				Возврат Результат;
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции


// получить данные из контейнера и записать в файл
Функция ПолучитьФайл(Позиция) Экспорт

	ИмяФайла = ОбъединитьПути(КаталогФайловДанных, Позиция);

	ФайлДанных = Новый Файл(ИмяФайла);

	Если НЕ ФайлДанных.Существует() Тогда

		Попытка

			ОткрытьПотокДанных(Ложь, Позиция);

			Буфер = Новый БуферДвоичныхДанных(4);
			ПотокДанных.Прочитать(Буфер, 0, 4);
			ОбъемДанных = Буфер.ПрочитатьЦелое32(0);

			Буфер = Новый БуферДвоичныхДанных(ОбъемДанных);
			ПотокДанных.Прочитать(Буфер, 0, ОбъемДанных);

			ДанныеФайла = ФайловыеПотоки.ОткрытьДляЗаписи(ИмяФайла);
			ДанныеФайла.Записать(Буфер, 0, ОбъемДанных);

			ДанныеФайла.Закрыть();

		Исключение

			Сообщить(ОписаниеОшибки());
			Возврат Ложь;

		КонецПопытки

	КонецЕсли;

	Возврат Истина;

КонецФункции


// Удалить данные из контейнера (заголовок тип + 10000)
Функция УдалитьДанные(Позиция) Экспорт

	ОткрытьПотокДанных(Ложь, Позиция);

	Буфер = Новый БуферДвоичныхДанных(4);
	ПотокДанных.Прочитать(Буфер, 0, 4);
	ОбъемДанных = Буфер.ПрочитатьЦелое32(0);

	Позиция = Позиция + ОбъемДанных + 4 + 12;

	ОткрытьПотокДанных(Ложь, Позиция); // это заголовок данных

	ПотокДанных.Прочитать(Буфер, 0, 4);
	ОбъемДанных = Буфер.ПрочитатьЦелое32(0);

	ОткрытьПотокДанных(Ложь, Позиция + ОбъемДанных + 4 + 8);

	ПотокДанных.Прочитать(Буфер, 0, 4);
	ТипДанных = Буфер.ПрочитатьЦелое32(0);

	ОткрытьПотокДанных(Истина, Позиция + ОбъемДанных + 4 + 8);
	Буфер.ЗаписатьЦелое32(0, 10000 + ТипДанных);
	ПотокДанных.Записать(Буфер, 0, 4);

	Возврат "Успешно";

КонецФункции


// Получить двоичные данные из контейнера
Функция ПолучитьДанные(Позиция, ТипДанных = Неопределено) Экспорт

	ОткрытьПотокДанных(Ложь, Позиция);

	Буфер = Новый БуферДвоичныхДанных(4);
	ПотокДанных.Прочитать(Буфер, 0, 4);
	ОбъемДанных = Буфер.ПрочитатьЦелое32(0);

	Буфер = Новый БуферДвоичныхДанных(ОбъемДанных);
	ПотокДанных.Прочитать(Буфер, 0, ОбъемДанных);

	Если НЕ ТипДанных = Неопределено Тогда // прочитать тип данных
		Буфер1 = Новый БуферДвоичныхДанных(4);
		ПотокДанных.Прочитать(Буфер1, 0, 4);
		ПотокДанных.Прочитать(Буфер1, 0, 4);
		ПотокДанных.Прочитать(Буфер1, 0, 4);
		ТипДанных = Буфер1.ПрочитатьЦелое32(0);
	КонецЕсли;

	Возврат ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);

КонецФункции


// добавить в контейнер заголовок с файлом или без
Функция ДобавитьДанные(Заголовок, дДанные = Неопределено) Экспорт

	Перем ИмяФайла, ТипДанных;

	ОткрытьПотокДанных(Истина);

	ПозицияДанных = ПотокДанных.ТекущаяПозиция();

	Если Заголовок.Свойство("ТипДанных", ТипДанных) И НЕ дДанные = Неопределено Тогда
		Заголовок.Вставить("ОбъемДанных", ЗаписатьДанные(дДанные, Число(ТипДанных)));
		Заголовок.Вставить("ПозицияДанных", ПозицияДанных);
	КонецЕсли;

	Заголовок.Вставить("Дата", "" + ТекущаяДата());

	дЗаголовок = СтруктуруВДвоичныеДанные(Заголовок);

	ЗаписатьДанные(дЗаголовок);

	Возврат ПозицияДанных;

КонецФункции


// добавить содержимое файла в контейнер
Функция ЗаписатьДанныеФайла(ИмяФайла)

	ПозицияДанных = ПотокДанных.ТекущаяПозиция();

	ДанныеФайла = ФайловыеПотоки.ОткрытьДляЧтения(ИмяФайла);
	ОбъемДанных = ДанныеФайла.Размер();

	Буфер = Новый БуферДвоичныхДанных(4);
	Буфер.ЗаписатьЦелое32(0, ОбъемДанных);
	ПотокДанных.Записать(Буфер, 0, 4);

	ДанныеФайла.КопироватьВ(ПотокДанных);
	ДанныеФайла.Закрыть();

	Буфер = Новый БуферДвоичныхДанных(8);
	Буфер.ЗаписатьЦелое64(0, ПозицияДанных);
	ПотокДанных.Записать(Буфер, 0, 8);

	ТипДанных = 2; // 2 = файл

	Буфер = Новый БуферДвоичныхДанных(4);
	Буфер.ЗаписатьЦелое32(0, ТипДанных);
	ПотокДанных.Записать(Буфер, 0, 4);

	ПотокДанных.СброситьБуферы();

	Возврат ОбъемДанных;

КонецФункции


// добавить двоичные данные в контейнер
Функция ЗаписатьДанные(дДанные, ТипДанных = 1)

	ПозицияДанных = ПотокДанных.ТекущаяПозиция();

	ОбъемДанных = дДанные.Размер();

	Буфер = Новый БуферДвоичныхДанных(4);
	Буфер.ЗаписатьЦелое32(0, ОбъемДанных);
	ПотокДанных.Записать(Буфер, 0, 4);

	Буфер = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(дДанные);
	ПотокДанных.Записать(Буфер, 0, ОбъемДанных);

	Буфер = Новый БуферДвоичныхДанных(8);
	Буфер.ЗаписатьЦелое64(0, ПозицияДанных);
	ПотокДанных.Записать(Буфер, 0, 8);

	Буфер = Новый БуферДвоичныхДанных(4);
	Буфер.ЗаписатьЦелое32(0, ТипДанных); // 1 = заголовок
	ПотокДанных.Записать(Буфер, 0, 4);

	ПотокДанных.СброситьБуферы();

	Возврат ОбъемДанных;

КонецФункции


Функция ПриСозданииОбъекта(КаталогФайловДанных, значИмяФайлаДанных);
	ИмяФайлаДанных = ОбъединитьПути(КаталогФайловДанных, ЗначИмяФайлаДанных  + ".sdb");
	Файл = Новый Файл(ИмяФайлаДанных);
	Если НЕ Файл.Существует() Тогда
		Файл = Новый ТекстовыйДокумент;
		Файл.Записать(ИмяФайлаДанных);
		Файл = Новый Файл(ИмяФайлаДанных);
	КонецЕсли;
	ВремяИзменения = Файл.ПолучитьВремяИзменения();
КонецФункции
