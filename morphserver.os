// /*----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------*/

Перем Хост, Порт;
Перем ОстановитьСервер;
Перем МоментЗапуска;
Перем Задачи, мЗадачи;
Перем Соединения;
Перем Леммы;
Перем Формы;
Перем Связи;

Функция ПолучитьИД()
	МоментЗапуска = МоментЗапуска - 1;
	Возврат Цел(ТекущаяУниверсальнаяДатаВМиллисекундах() - МоментЗапуска);
КонецФункции // ПолучитьИД()


Функция СтруктуруВДвоичныеДанные(знСтруктура)
	Результат = Новый Массив;
	Если НЕ знСтруктура = Неопределено Тогда
		Для каждого Элемент Из знСтруктура Цикл
			Ключ = Элемент.Ключ;
			Значение = Элемент.Значение;
			Если ТипЗнч(Значение) = Тип("Структура") Тогда
				Ключ = "*" + Ключ;
				дЗначение = СтруктуруВДвоичныеДанные(Значение);
			ИначеЕсли ТипЗнч(Значение) = Тип("ДвоичныеДанные") Тогда
				Ключ = "#" + Ключ;
				дЗначение = Значение;
			Иначе
				дЗначение = ПолучитьДвоичныеДанныеИзСтроки(Значение);
			КонецЕсли;
			дКлюч = ПолучитьДвоичныеДанныеИзСтроки(Ключ);
			рдКлюч = дКлюч.Размер();
			рдЗначение = дЗначение.Размер();
			бРезультат = Новый БуферДвоичныхДанных(6);
			бРезультат.ЗаписатьЦелое16(0, рдКлюч);
			бРезультат.ЗаписатьЦелое32(2, рдЗначение);
			Результат.Добавить(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бРезультат));
			Результат.Добавить(дКлюч);
			Результат.Добавить(дЗначение);
		КонецЦикла;
	КонецЕсли;
	Возврат СоединитьДвоичныеДанные(Результат);
КонецФункции


Функция ДвоичныеДанныеВСтруктуру(Данные, Рекурсия = Истина)
	Если ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
		рдДанные = Данные.Размер();
		Если рдДанные = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		бдДанные = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(Данные);
	ИначеЕсли ТипЗнч(Данные) = Тип("БуферДвоичныхДанных") Тогда
		рдДанные = Данные.Размер;
		бдДанные = Данные;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	Позиция = 0;
	знСтруктура = Новый Структура;
	Пока Позиция < рдДанные - 1 Цикл
		рдКлюч = бдДанные.ПрочитатьЦелое16(Позиция);
		рдЗначение = бдДанные.ПрочитатьЦелое32(Позиция + 2);
		Если рдКлюч + рдЗначение > рдДанные Тогда // Это не структура
			Возврат Неопределено;
		КонецЕсли;
		Ключ = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бдДанные.Прочитать(Позиция + 6, рдКлюч)));
		бЗначение = бдДанные.Прочитать(Позиция + 6 + рдКлюч, рдЗначение);
		Позиция = Позиция + 6 + рдКлюч + рдЗначение;
		Л = Лев(Ключ, 1);
		Если Л = "*" Тогда
			Если НЕ Рекурсия Тогда
				Продолжить;
			КонецЕсли;
			Ключ = Сред(Ключ, 2);
			Значение = ДвоичныеДанныеВСтруктуру(бЗначение);
		ИначеЕсли Л = "#" Тогда
			Ключ = Сред(Ключ, 2);
			Значение = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бЗначение);
		Иначе
			Значение = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бЗначение));
		КонецЕсли;
		знСтруктура.Вставить(Ключ, Значение);
	КонецЦикла;
	Возврат знСтруктура;
КонецФункции


Функция ПередатьДанные(Хост, Порт, стрДанные) Экспорт
	Попытка
		Соединение = Новый TCPСоединение(Хост, Порт);
		Соединение.ТаймаутОтправки = 5000;
		Соединение.ОтправитьДвоичныеДанныеАсинхронно(СтруктуруВДвоичныеДанные(стрДанные));
		Возврат Соединение;
	Исключение
		Сообщить(ОписаниеОшибки());
		Если Соединение = Неопределено Тогда
			Сообщить("dataserver: Хост недоступен: " + Хост + ":" + Порт);
		Иначе
			Соединение.Закрыть();
			Соединение = Неопределено;
		КонецЕсли;
	КонецПопытки;
	Возврат Соединение;
КонецФункции // ПередатьДанные()


Функция ОткрытьПоток(Запись)

	Если НЕ Связи = Неопределено Тогда
		Связи.Закрыть();
	КонецЕсли;

	Если Запись = 1 Тогда
		Связи =	ФайловыеПотоки.ОткрытьДляЗаписи(ОбъединитьПути(ТекущийКаталог(), "morph", "Связи.dat"));
	Иначе
		Связи =	ФайловыеПотоки.ОткрытьДляЧтения(ОбъединитьПути(ТекущийКаталог(), "morph", "Связи.dat"));
	КонецЕсли;

КонецФункции // ОткрытьПоток()


Функция ПолучитьЗначения(Знач н, Знач ф, мф = Неопределено, Все = Ложь)
	рез = Неопределено;
	буф = Новый БуферДвоичныхДанных(10);
	Пока Истина Цикл
		Связи.Перейти(н, ПозицияВПотоке.Начало);
		Связи.Прочитать(буф, 0, 10);
		ф1 = буф.ПрочитатьЦелое16(0);
		нн = буф.ПрочитатьЦелое32(2); // позиция следующего
		вн = буф.ПрочитатьЦелое32(6); // позиция вложенного
		Если НЕ вн = 0 И НЕ (ф1 = 9 И НЕ Все) Тогда // есть вложение
			рез = ПолучитьЗначения(вн, ф + Символ(ф1), мф, Все);
		Иначе
			рез = ф + Символ(ф1);
			Если НЕ мф = Неопределено Тогда
				мф.Добавить(рез);
			КонецЕсли;
		КонецЕсли;
		н = нн;
		Если н = 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат рез;
КонецФункции // ПолучитьЗначения()


Функция НайтиЗначение(нгр)

	// пройти по дереву

	буф = Новый БуферДвоичныхДанных(10);

	ОткрытьПоток(0);

	н = 0;
	к = 1;

	Пока Истина Цикл
		ф = КодСимвола(Сред(нгр, к, 1));
		Пока Истина Цикл
			Связи.Перейти(н, ПозицияВПотоке.Начало);
			Связи.Прочитать(буф, 0, 10);
			ф1 = буф.ПрочитатьЦелое16(0);
			н = буф.ПрочитатьЦелое32(2); // позиция следующего
			Если ф1 = ф ИЛИ ф = 42 Тогда  // найден элемент
				Если ф = 42 Тогда // *
					Если ф1 = КодСимвола(Сред(нгр, к + 1, 1)) Тогда
						к = к + 1;
					КонецЕсли;
				Иначе
					к = к + 1;
				КонецЕсли;
				н = буф.ПрочитатьЦелое32(6); // позиция вложенного
				Прервать;
			КонецЕсли;
			Если н = 0 ИЛИ к > СтрДлина(нгр) Тогда // это последний
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если н = 0 ИЛИ к > СтрДлина(нгр) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат н;

КонецФункции // НайтиЗначение()


Функция УдалитьЗначение(нгр)

	// пройти по дереву

	буф = Новый БуферДвоичныхДанных(10);

	ОткрытьПоток(0);

	н = 0;
	к = 1;

	Пока Истина Цикл
		ф = КодСимвола(Сред(нгр, к, 1));
		Пока Истина Цикл
			Связи.Перейти(н, ПозицияВПотоке.Начало);
			Связи.Прочитать(буф, 0, 10);
			ф1 = буф.ПрочитатьЦелое16(0);
			нн = буф.ПрочитатьЦелое32(2); // позиция следующего
			Если ф1 = ф ИЛИ ф = 42 Тогда  // найден элемент
				Если ф = 42 Тогда // *
					Если ф1 = КодСимвола(Сред(нгр, к + 1, 1)) Тогда
						к = к + 1;
					КонецЕсли;
				Иначе
					к = к + 1;
				КонецЕсли;
				нн = буф.ПрочитатьЦелое32(6); // позиция вложенного
				Прервать;
			КонецЕсли;
			Если нн = 0 ИЛИ к > СтрДлина(нгр) Тогда // это последний
				Прервать;
			КонецЕсли;
			н = нн;
		КонецЦикла;
		Если нн = 0 ИЛИ к > СтрДлина(нгр) Тогда
			Прервать;
		КонецЕсли;
		н = нн;
	КонецЦикла;

	Если НЕ н = 0 И к > СтрДлина(нгр) Тогда
		буф.ЗаписатьЦелое16(0, 0);
		ОткрытьПоток(1);
		Связи.Перейти(н, ПозицияВПотоке.Начало);
		Связи.Записать(буф, 0, 10);
		Связи.СброситьБуферы();
	КонецЕсли;

КонецФункции // УдалитьЗначение()


Функция ДобавитьЗначения(гр)

	// пройти по дереву
	ОткрытьПоток(0);
	буф = Новый БуферДвоичныхДанных(10);

	н = 0;
	к = 1;

	Если НЕ Связи.Размер() = 0 Тогда

		Пока Истина Цикл
			ф = КодСимвола(Сред(гр, к, 1));
			Пока Истина Цикл
				Связи.Перейти(н, ПозицияВПотоке.Начало);
				Связи.Прочитать(буф, 0, 10);
				ф1 = буф.ПрочитатьЦелое16(0);
				нн = буф.ПрочитатьЦелое32(2); // позиция следующего
				Если ф1 = ф Тогда  // найден элемент
					нн = буф.ПрочитатьЦелое32(6); // позиция вложенного
					Если нн = 0 И НЕ к = СтрДлина(гр) Тогда // создать ссылку на вложенный
						кн = Связи.Размер();
						ОткрытьПоток(1);
						буф.ЗаписатьЦелое32(6, кн); // вложенный в конец
						Связи.Перейти(н, ПозицияВПотоке.Начало);
						Связи.Записать(буф, 0, 10);
						Связи.СброситьБуферы();
					КонецЕсли;
					к = к + 1;
					н = нн;
					Прервать;
				ИначеЕсли нн = 0 И НЕ к = СтрДлина(гр) Тогда // это последний
					ОткрытьПоток(1);
					кн = Связи.Размер();
					буф.ЗаписатьЦелое32(2, кн); // соседний в конец
					Связи.Перейти(н, ПозицияВПотоке.Начало);
					Связи.Записать(буф, 0, 10);
					Связи.СброситьБуферы();
				КонецЕсли;
				Если нн = 0 Тогда
					Прервать;
				КонецЕсли;
				н = нн;
			КонецЦикла;
			Если нн = 0 ИЛИ к > СтрДлина(гр) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;

	// создать новые элементы
	ОткрытьПоток(1);

	Для к = к по СтрДлина(гр) Цикл
		ф = КодСимвола(Сред(гр, к, 1));
		Связи.Перейти(0, ПозицияВПотоке.Конец);
		Если НЕ к = СтрДлина(гр) Тогда
			кн = Связи.ТекущаяПозиция() + 10;
		Иначе
			кн = 0;
		КонецЕсли;
		буф.ЗаписатьЦелое16(0, ф); // код символа
		буф.ЗаписатьЦелое32(2, 0); // нет соседнего
		буф.ЗаписатьЦелое32(6, кн); // вложенный в конец
		Связи.Записать(буф, 0, 10);
		Связи.СброситьБуферы();
	КонецЦикла;

КонецФункции // ДобавитьЗначения()


Функция ВыполнитьЗадачу(структЗадача)

	Перем Команда;

	структЗадача.Запрос.Свойство("cmd", Команда);

	Если Команда = "stopserver" Тогда
		ОстановитьСервер = Истина;
		Возврат Ложь;

	ИначеЕсли Команда = "ФормыСлов" Тогда

		мСлова = СтрРазделить(структЗадача.Запрос.Слова, Символы.ПС);

		Если структЗадача.Результат.Количество() = мСлова.Количество() Тогда
			Возврат Истина;
		КонецЕсли;

		сл = мСлова.Получить(структЗадача.Результат.Количество());
		слф = ВРег(сл);
		рез = "";

		буф = Новый БуферДвоичныхДанных(2);
		бп = Новый БуферДвоичныхДанных(4);

		Сообщить(слф);

		н = 0;
		ф1 = 1;
		Для к = 1 по СтрДлина(слф) Цикл
			ф = КодСимвола(Сред(слф, к, 1));
			ф1 = 0;
			Пока Истина Цикл
				Формы.Перейти(н, ПозицияВПотоке.Начало);
				Формы.Прочитать(буф, 0, 2);
				ф1 = буф.ПрочитатьЦелое16(0);
				Если ф1 = 1 Тогда
					Прервать;
				ИначеЕсли ф1 = 0 Тогда
					Формы.Прочитать(бп, 0, 4);
					н = н + 2 + 4 + бп.ПрочитатьЦелое32(0);
				ИначеЕсли ф1 = ф Тогда
					н = н + 2 + 4;
					Прервать;
				Иначе
					Формы.Прочитать(бп, 0, 4);
					н = бп.ПрочитатьЦелое32(0);
				КонецЕсли;
			КонецЦикла;
			Если ф1 = 1 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если НЕ ф1 = 1 Тогда
			Пока Истина Цикл
				Формы.Перейти(н, ПозицияВПотоке.Начало);
				Формы.Прочитать(буф, 0, 2);
				ф1 = буф.ПрочитатьЦелое16(0);
				Если ф1 = 1 Тогда
					Прервать;
				ИначеЕсли ф1 = 0 Тогда
					Формы.Прочитать(бп, 0, 4);
					рд = бп.ПрочитатьЦелое32(0);
					бд = Новый БуферДвоичныхДанных(рд);
					Формы.Прочитать(бд, 0, рд);
					дд = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бд);
					стр = ПолучитьСтрокуИзДвоичныхДанных(дд);
					фс = СтрРазделить(стр, Символы.Таб);
					нФорма = фс[0];
					нл = фс[1];
					фл = фс[2];
					рез = рез + ?(рез = "", "", Символы.ПС) + слф + Символы.Таб + Леммы.Получить(Число(фл)) + Символы.Таб + нФорма + Символы.Таб + Леммы.Получить(Число(нл));
					н = н + 2 + 4 + рд;
				Иначе
					Формы.Прочитать(бп, 0, 4);
					н = бп.ПрочитатьЦелое32(0);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

		Если рез = "" Тогда // форма слова не найдена
			Если СтрНайти(слф, "Ё") = 0 Тогда // е заменить на ё
				Для н = 1 по СтрДлина(слф) Цикл
					Если Сред(слф, н, 1) = "Е" Тогда
						структЗадача.Запрос.Слова = структЗадача.Запрос.Слова + Символы.ПС + Лев(слф, н - 1) + "Ё" + Сред(слф, н + 1);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;

		структЗадача.Результат.Вставить("s_" + структЗадача.Результат.Количество(), Новый Структура("Слово, Результат", сл, рез));

		Возврат Ложь;

	ИначеЕсли Команда = "Связи" Тогда

		мСлова = СтрРазделить(структЗадача.Запрос.Слова, Символы.ПС);

		Для каждого бигр Из мСлова Цикл

			мб = СтрРазделить(бигр, Символы.Таб);

			ток1имя = мб[0];
			ток1нач = мб[1];
			ток1гр = мб[2];
			свимя = мб[3];
			ток2имя = мб[4];
			ток2нач = мб[5];
			ток2гр = мб[6];
			р = мб[7];

			Если р = "" Тогда // удалить связь

				св = Символы.Таб +
					ток1нач + Символы.Таб +
					ток2нач + Символы.Таб +
					ВРег(свимя);

				УдалитьЗначение(св);

			ИначеЕсли р = "г" Тогда // удалить грамматику

				// удалить грамматику
				гр =
					ток1гр + Символы.Таб +
					ток2гр + Символы.Таб +
					ВРег(свимя);

				УдалитьЗначение(гр);

				// удалить связь
				св = Символы.Таб +
					ток1нач + Символы.Таб +
					ток2нач + Символы.Таб +
					ВРег(свимя);

				УдалитьЗначение(св);

			ИначеЕсли р = "+" Тогда // добавить корректную связь

				// удалить некорректную связь
				св = Символы.Таб +
					ток1нач + Символы.Таб +
					ток2нач + Символы.Таб +
					ВРег(свимя) + Символы.Таб +
					"-";

				УдалитьЗначение(св);

				// добавить грамматику
				гр =
					ток1гр + Символы.Таб +
					ток2гр + Символы.Таб +
					ВРег(свимя);

				ДобавитьЗначения(гр);

				// добавить связь
				св = Символы.Таб +
					ток1нач + Символы.Таб +
					ток2нач + Символы.Таб +
					ВРег(свимя) + Символы.Таб +
					р;

				ДобавитьЗначения(св);

			ИначеЕсли р = "-" Тогда // добавить некорректную биграмму

				// удалить корректную связь
				св = Символы.Таб +
					ток1нач + Символы.Таб +
					ток2нач + Символы.Таб +
					ВРег(свимя) + Символы.Таб +
					"+";

				УдалитьЗначение(св);

				// добавить связь
				св = Символы.Таб +
					ток1нач + Символы.Таб +
					ток2нач + Символы.Таб +
					ВРег(свимя) + Символы.Таб +
					р;

				ДобавитьЗначения(св);

			КонецЕсли;

			структЗадача.Результат.Вставить(ток1имя + "_" + ток2имя, р);

		КонецЦикла;

		Возврат Истина;

	ИначеЕсли Команда = "Грамматики" Тогда

		сгр = Новый Соответствие;

		сл = структЗадача.Запрос.Слова;

		мсл = СтрРазделить(сл, Символы.ПС);

		Для каждого гр Из мсл Цикл

			мгр = СтрРазделить(гр, Символы.Таб);

			ток1гр = мгр[0];
			ток2гр = мгр[1];
			ток1нач = мгр[2];
			ток2нач = мгр[3];

			Если НЕ сгр.Получить(гр) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			сгр.Вставить(гр, "");

			н = НайтиЗначение(ток1гр + Символы.Таб + ток2гр + Символы.Таб);

			Если НЕ н = 0 Тогда // найдено совпадение по образцу
				мф = Новый Массив;
				ПолучитьЗначения(н, "", мф);
				Для каждого г2 Из мф Цикл

					б = "";

					// найти связь
					нсв = Символы.Таб +
						ток1нач + Символы.Таб +
						ток2нач + Символы.Таб +
						ВРег(г2) + Символы.Таб;

					н = НайтиЗначение(нсв);

					Если НЕ н = 0 Тогда // найдено совпадение по образцу
						св = ПолучитьЗначения(н, "");
						Если НЕ св = Неопределено Тогда
							б = св;
						КонецЕсли;
					КонецЕсли;

					структЗадача.Результат.Вставить("гр_" + структЗадача.Результат.Количество(), гр + "_" + г2 + Символы.Таб + б);

				КонецЦикла;
			КонецЕсли;

		КонецЦикла;

		Возврат Истина;

	КонецЕсли;

КонецФункции


Процедура ОбработатьСоединения() Экспорт

	Если АргументыКоманднойСтроки.Количество() Тогда
		Порт = Число(АргументыКоманднойСтроки[0]);
	Иначе
		Порт = 8886;
	КонецЕсли;

	Таймаут = 50;

	TCPСервер = Новый TCPСервер(Порт);
	TCPСервер.ЗапуститьАсинхронно();

	Сообщить(СокрЛП(ТекущаяДата()) + " Сервер морфологии запущен на порту: " + Порт);

	Леммы = Новый Соответствие;
	ф = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Леммы.dat"));
	Пока Истина Цикл
		кл = ф.ПрочитатьСтроку();
		Если кл = Неопределено Тогда
			Прервать;
		КонецЕсли;
		зн = ф.ПрочитатьСтроку();
		Леммы.Вставить(Число(кл), зн);
	КонецЦикла;
	Сообщить("Прочитал Леммы");
	ф.Закрыть();

	Формы =	ФайловыеПотоки.ОткрытьДляЧтения(ОбъединитьПути(ТекущийКаталог(), "morph", "Формы.dat"));

	Задачи = Новый Соответствие;
	мЗадачи = Новый Массив;

	ОстановитьСервер = Ложь;
	ПерезапуститьСервер = Ложь;
	Соединение = Неопределено;

	Соединения = Новый Массив();

	СуммаЦиклов = 0;
	РабочийЦикл = 0;
	ЗамерВремени = ТекущаяУниверсальнаяДатаВМиллисекундах();

	Пока НЕ ОстановитьСервер Цикл

		НачалоЦикла = ТекущаяУниверсальнаяДатаВМиллисекундах();
		СуммаЦиклов = СуммаЦиклов + 1;

		Если СуммаЦиклов > 999 Тогда
			ПредЗамер = ЗамерВремени;
			ЗамерВремени = ТекущаяУниверсальнаяДатаВМиллисекундах();
			Загрузка = " " + РабочийЦикл / 10 + "% " + Цел(1000 * РабочийЦикл / (ЗамерВремени - ПредЗамер)) + " q/s " + Задачи.Количество() + " tasks";
			СуммаЦиклов = 0;
			РабочийЦикл = 0;
		КонецЕсли;

		к = мЗадачи.Количество();
		Пока к > 0 И НЕ ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЦикла > 50 Цикл
			к = к - 1;
			структЗадача = мЗадачи.Получить(0);
			мЗадачи.Удалить(0);

			ЕстьРезультат = Ложь;
			РабочийЦикл = РабочийЦикл + 1;
			Попытка
				ЕстьРезультат = ВыполнитьЗадачу(структЗадача);
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
			Если ЕстьРезультат = Истина Тогда
				Попытка
					ОбратныйЗапрос = "";
					Если структЗадача.Запрос.Свойство("ОбратныйЗапрос", ОбратныйЗапрос) Тогда // возвращаем результат
						ОбратныйЗапрос.Вставить("РезультатДанные", Новый Структура("Ответ, Результат", структЗадача.Ответ, структЗадача.Результат));
						Если ПередатьДанные(ОбратныйЗапрос.Хост, ОбратныйЗапрос.Порт, ОбратныйЗапрос) = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						Сообщить("morphserver " + ТекущаяДата() + " time=" + (ТекущаяДата() - структЗадача.ВремяНачало) + Загрузка);
						структЗадача.Результат = Неопределено;
					КонецЕсли;
				Исключение
					Сообщить(ОписаниеОшибки());
				КонецПопытки;
				Задачи.Удалить(структЗадача.ИдЗадачи);
				//Сообщить("morphserver: всего задач " + Задачи.Количество());
				Продолжить;
			КонецЕсли;

			мЗадачи.Добавить(структЗадача);

		КонецЦикла;

		т = Таймаут;
		Пока Истина Цикл
			Соединение = TCPСервер.ПолучитьСоединение(т);
			Если Соединение = Неопределено Тогда
				Прервать;
			КонецЕсли;
			Соединения.Вставить(0, Соединение);
			т = 0;
		КонецЦикла;

		к = Соединения.Количество();
		Пока к > 0 Цикл
			к = к - 1;
			Соединение = Соединения.Получить(0);
			Соединения.Удалить(0);

			Если Соединение.Статус = "Успех" Тогда

				Попытка
					Запрос = Неопределено;
					Запрос = ДвоичныеДанныеВСтруктуру(Соединение.ПолучитьДвоичныеДанные());
				Исключение
					Сообщить("morphserver: " + ОписаниеОшибки());
				КонецПопытки;

				Если НЕ Запрос = Неопределено Тогда
					структЗадача = Новый Структура("ИдЗадачи, Запрос, Ответ, Результат, ВремяНачало", ПолучитьИД(), Запрос, Неопределено, Новый Структура(), ТекущаяДата());
					Задачи.Вставить(структЗадача.ИдЗадачи, структЗадача);
					мЗадачи.Добавить(структЗадача);
					//Сообщить("dataserver: всего задач " + Задачи.Количество());
				КонецЕсли;

				Соединение.Закрыть();
				Продолжить;

			ИначеЕсли Соединение.Статус = "Ошибка" Тогда

				Соединение.Закрыть();
				Продолжить;

			КонецЕсли;

			Соединения.Добавить(Соединение);

		КонецЦикла;

		ВремяЦикла = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЦикла;
		Если ВремяЦикла > 100 Тогда
			Сообщить("!morphserver ВремяЦикла=" + ВремяЦикла);
		КонецЕсли;
		Таймаут = ?(ВремяЦикла > 50, 0, 30);

	КонецЦикла;

	Если НЕ Связи = Неопределено Тогда
		Связи.Закрыть();
	КонецЕсли;

	TCPСервер.Остановить();
	Сообщить("Завершил работу сервера морфологии.");

КонецПроцедуры

МоментЗапуска = ТекущаяУниверсальнаяДатаВМиллисекундах();
ОбработатьСоединения();
