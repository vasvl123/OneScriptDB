// /*----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------*/

Перем Хост, Порт;
Перем ОстановитьСервер;
Перем МоментЗапуска;
Перем Задачи, мЗадачи;
Перем Соединения;
Перем Леммы;
Перем Связи;
Перем НачальныеФормы;
Перем Правила;
Перем Биграммы;
Перем Грамматики;

Функция ПолучитьИД()
	МоментЗапуска = МоментЗапуска - 1;
	Возврат Цел(ТекущаяУниверсальнаяДатаВМиллисекундах() - МоментЗапуска);
КонецФункции // ПолучитьИД()


Функция СтруктуруВДвоичныеДанные(знСтруктура)
	Результат = Новый Массив;
	Если НЕ знСтруктура = Неопределено Тогда
		Для каждого Элемент Из знСтруктура Цикл
			Ключ = Элемент.Ключ;
			Значение = Элемент.Значение;
			Если ТипЗнч(Значение) = Тип("Структура") Тогда
				Ключ = "*" + Ключ;
				дЗначение = СтруктуруВДвоичныеДанные(Значение);
			ИначеЕсли ТипЗнч(Значение) = Тип("ДвоичныеДанные") Тогда
				Ключ = "#" + Ключ;
				дЗначение = Значение;
			Иначе
				дЗначение = ПолучитьДвоичныеДанныеИзСтроки(Значение);
			КонецЕсли;
			дКлюч = ПолучитьДвоичныеДанныеИзСтроки(Ключ);
			рдКлюч = дКлюч.Размер();
			рдЗначение = дЗначение.Размер();
			бРезультат = Новый БуферДвоичныхДанных(6);
			бРезультат.ЗаписатьЦелое16(0, рдКлюч);
			бРезультат.ЗаписатьЦелое32(2, рдЗначение);
			Результат.Добавить(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бРезультат));
			Результат.Добавить(дКлюч);
			Результат.Добавить(дЗначение);
		КонецЦикла;
	КонецЕсли;
	Возврат СоединитьДвоичныеДанные(Результат);
КонецФункции


Функция ДвоичныеДанныеВСтруктуру(Данные, Рекурсия = Истина)
	Если ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
		рдДанные = Данные.Размер();
		Если рдДанные = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		бдДанные = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(Данные);
	ИначеЕсли ТипЗнч(Данные) = Тип("БуферДвоичныхДанных") Тогда
		рдДанные = Данные.Размер;
		бдДанные = Данные;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	Позиция = 0;
	знСтруктура = Новый Структура;
	Пока Позиция < рдДанные - 1 Цикл
		рдКлюч = бдДанные.ПрочитатьЦелое16(Позиция);
		рдЗначение = бдДанные.ПрочитатьЦелое32(Позиция + 2);
		Если рдКлюч + рдЗначение > рдДанные Тогда // Это не структура
			Возврат Неопределено;
		КонецЕсли;
		Ключ = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бдДанные.Прочитать(Позиция + 6, рдКлюч)));
		бЗначение = бдДанные.Прочитать(Позиция + 6 + рдКлюч, рдЗначение);
		Позиция = Позиция + 6 + рдКлюч + рдЗначение;
		Л = Лев(Ключ, 1);
		Если Л = "*" Тогда
			Если НЕ Рекурсия Тогда
				Продолжить;
			КонецЕсли;
			Ключ = Сред(Ключ, 2);
			Значение = ДвоичныеДанныеВСтруктуру(бЗначение);
		ИначеЕсли Л = "#" Тогда
			Ключ = Сред(Ключ, 2);
			Значение = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бЗначение);
		Иначе
			Значение = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(бЗначение));
		КонецЕсли;
		знСтруктура.Вставить(Ключ, Значение);
	КонецЦикла;
	Возврат знСтруктура;
КонецФункции


Функция ПередатьДанные(Хост, Порт, стрДанные) Экспорт
	Попытка
		Соединение = Новый TCPСоединение(Хост, Порт);
		Соединение.ТаймаутОтправки = 5000;
		Соединение.ОтправитьДвоичныеДанныеАсинхронно(СтруктуруВДвоичныеДанные(стрДанные));
		Возврат Соединение;
	Исключение
		Сообщить(ОписаниеОшибки());
		Если Соединение = Неопределено Тогда
			Сообщить("dataserver: Хост недоступен: " + Хост + ":" + Порт);
		Иначе
			Соединение.Закрыть();
			Соединение = Неопределено;
		КонецЕсли;
	КонецПопытки;
	Возврат Соединение;
КонецФункции // ПередатьДанные()


Функция ВыполнитьЗадачу(структЗадача)

	Перем Команда;

	структЗадача.Запрос.Свойство("cmd", Команда);

	Если Команда = "stopserver" Тогда
		ОстановитьСервер = Истина;
		Возврат Ложь;

	ИначеЕсли Команда = "ФормыСлов" Тогда

		мСлова = СтрРазделить(структЗадача.Запрос.Слова, Символы.ПС);

		Если структЗадача.Результат.Количество() = мСлова.Количество() Тогда
			Возврат Истина;
		КонецЕсли;

		сл = мСлова.Получить(структЗадача.Результат.Количество());
		слф = ВРег(сл);
		рез = "";

		Для н = 0 по стрДлина(слф) Цикл

			Если н > 0 Тогда
				ок = Прав(слф, н);
				нач = Лев(слф, СтрДлина(слф) - н);

				к = 0;
				Пока Истина Цикл
					фс = Правила.Получить(ок + "_" + к);
					Если фс = Неопределено Тогда
						Прервать;
					КонецЕсли;
					фс = СтрРазделить(фс, Символы.Таб);
					м = 0;
					Пока Истина Цикл
						нф = НачальныеФормы.Получить(нач + фс[0] + "_" + м);
						Если нф = Неопределено Тогда
							Прервать;
						КонецЕсли;
						л = 0;
						Пока Истина Цикл
							нф1 = Связи.Получить(фс[1] + "_" + л);
							Если нф1 = Неопределено Тогда
								Прервать;
							КонецЕсли;
							Если нф1 = нф Тогда
								рез = рез + ?(рез = "", "", Символы.ПС) + слф + Символы.Таб + Леммы.Получить(Число(фс[1])) + Символы.Таб + нач + фс[0] + Символы.Таб + Леммы.Получить(Число(нф));
							КонецЕсли;
							л = л + 1;
						КонецЦикла;
						м = м + 1;
					КонецЦикла;
					к = к + 1;
				КонецЦикла;

			Иначе

				м = 0;
				Пока Истина Цикл
					нф = НачальныеФормы.Получить(слф + "_" + м);
					Если нф = Неопределено Тогда
						Прервать;
					КонецЕсли;
					рез = рез + ?(рез = "", "", Символы.ПС) + слф + Символы.Таб + Леммы.Получить(Число(нф));
					м = м + 1;
				КонецЦикла;

			КонецЕсли;

		КонецЦикла;

		структЗадача.Результат.Вставить("s_" + структЗадача.Результат.Количество(), Новый Структура("Слово, Результат", сл, рез));

		Возврат Ложь;

	ИначеЕсли Команда = "Биграммы" Тогда

		мСлова = СтрРазделить(структЗадача.Запрос.Слова, Символы.ПС);

		Для каждого бигр Из мСлова Цикл

			мб = СтрРазделить(бигр, Символы.Таб);

			мс = Биграммы.Получить(мб[1]); // начальная форма
			Если мс = Неопределено Тогда
				мс = Новый Массив;
				Биграммы.Вставить(мб[1], мс);
			КонецЕсли;

			р = "";
			Если мб.Количество() = 5 Тогда
				р = мб[4];
				Если р = "+" Тогда // добавить корректную биграмму
					Если мс.Найти(мб[3]) = Неопределено Тогда
						мс.Добавить(мб[3]);
					КонецЕсли;
					// удалить из некорректных
					п = мс.Найти("_" + мб[3]);
					Если НЕ п = Неопределено Тогда
						мс.Удалить(п);
					КонецЕсли;
				ИначеЕсли р = "-" Тогда // добавить некорректную биграмму
					Если мс.Найти("_" + мб[3]) = Неопределено Тогда
						мс.Добавить("_" + мб[3]);
					КонецЕсли;
					// удалить из корректных
					п = мс.Найти(мб[3]);
					Если НЕ п = Неопределено Тогда
						мс.Удалить(п);
					КонецЕсли;
				КонецЕсли;

			Иначе // найти биграмму

				Если НЕ мс.Найти(мб[3]) = Неопределено Тогда
					р = "+";
				ИначеЕсли НЕ мс.Найти("_" + мб[3]) = Неопределено Тогда
					р = "-";
				КонецЕсли;

			КонецЕсли;

			структЗадача.Результат.Вставить(мб[0] + "_" + мб[2], р);

		КонецЦикла;

		Возврат Истина;

	ИначеЕсли Команда = "Грамматики" Тогда

		мСлова = СтрРазделить(структЗадача.Запрос.Слова, Символы.ПС);

		Для каждого гр Из мСлова Цикл

			мгр = СтрРазделить(гр, Символы.Таб);

			г1 = Грамматики.Получить(мгр[0]);
			Если НЕ г1 = Неопределено Тогда
				Для каждого г2 Из г1 Цикл
					г3 = Грамматики.Получить(г2);
					Для каждого г4 Из г3 Цикл
						Если г4 = мгр[1] Тогда
							структЗадача.Результат.Вставить(мгр, г2);
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;

		КонецЦикла;

		Возврат Истина;

	КонецЕсли;

КонецФункции


Процедура ОбработатьСоединения() Экспорт

	Если АргументыКоманднойСтроки.Количество() Тогда
		Порт = Число(АргументыКоманднойСтроки[0]);
	Иначе
		Порт = 8886;
	КонецЕсли;

	Таймаут = 50;

	TCPСервер = Новый TCPСервер(Порт);
	TCPСервер.ЗапуститьАсинхронно();

	Сообщить(СокрЛП(ТекущаяДата()) + " Сервер морфологии запущен на порту: " + Порт);

	Леммы = Новый Соответствие;
	ф = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Леммы.txt"));
	Пока Истина Цикл
		кл = ф.ПрочитатьСтроку();
		Если кл = Неопределено Тогда
			Прервать;
		КонецЕсли;
		зн = ф.ПрочитатьСтроку();
		Леммы.Вставить(Число(зн), кл);
	КонецЦикла;
	Сообщить("Прочитал Леммы");
	ф.Закрыть();

	Связи = Новый Соответствие;
	ф = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Связи.txt"));
	Пока Истина Цикл
		кл = ф.ПрочитатьСтроку();
		Если кл = Неопределено Тогда
			Прервать;
		КонецЕсли;
		зн = ф.ПрочитатьСтроку();
		Связи.Вставить(кл, Число(зн));
	КонецЦикла;
	Сообщить("Прочитал Связи");
	ф.Закрыть();

	НачальныеФормы = Новый Соответствие;
	ф = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "НачальныеФормы.txt"));
	Пока Истина Цикл
		кл = ф.ПрочитатьСтроку();
		Если кл = Неопределено Тогда
			Прервать;
		КонецЕсли;
		зн = ф.ПрочитатьСтроку();
		Если НЕ зн = "" Тогда
			НачальныеФормы.Вставить(кл, Число(зн));
		КонецЕсли;
	КонецЦикла;
	Сообщить("Прочитал НачальныеФормы");
	ф.Закрыть();

	Правила = Новый Соответствие;
	ф = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Правила.txt"));
	Пока Истина Цикл
		кл = ф.ПрочитатьСтроку();
		Если кл = Неопределено Тогда
			Прервать;
		КонецЕсли;
		зн = ф.ПрочитатьСтроку();
		Правила.Вставить(кл, зн);
	КонецЦикла;
	Сообщить("Прочитал Правила");
	ф.Закрыть();

	Биграммы = Новый Соответствие;
	ф = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Биграммы.txt"));
	Пока Истина Цикл
		гр1 = ф.ПрочитатьСтроку();
		Если гр1 = Неопределено Тогда
			Прервать;
		КонецЕсли;
		кол = Число(ф.ПрочитатьСтроку());
		гр2 = Новый Массив();
		Пока кол > 0 Цикл
			гр2.Добавить(ф.ПрочитатьСтроку());
			кол = кол - 1;
		КонецЦикла;
		Биграммы.Вставить(гр1, гр2);
	КонецЦикла;
	Сообщить("Прочитал Биграммы");
	ф.Закрыть();

	Грамматики = Новый Соответствие;
	ф = Новый ЧтениеТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Грамматики.txt"));
	Пока Истина Цикл
		гр1 = ф.ПрочитатьСтроку();
		Если гр1 = Неопределено Тогда
			Прервать;
		КонецЕсли;
		кол = Число(ф.ПрочитатьСтроку());
		гр2 = Новый Массив();
		Пока кол > 0 Цикл
			гр2.Добавить(ф.ПрочитатьСтроку());
			кол = кол - 1;
		КонецЦикла;
		Грамматики.Вставить(гр1, гр2);
	КонецЦикла;
	Сообщить("Прочитал Грамматики");
	ф.Закрыть();

	Задачи = Новый Соответствие;
	мЗадачи = Новый Массив;

	ОстановитьСервер = Ложь;
	ПерезапуститьСервер = Ложь;
	Соединение = Неопределено;

	Соединения = Новый Массив();

	СуммаЦиклов = 0;
	РабочийЦикл = 0;
	ЗамерВремени = ТекущаяУниверсальнаяДатаВМиллисекундах();

	Пока НЕ ОстановитьСервер Цикл

		НачалоЦикла = ТекущаяУниверсальнаяДатаВМиллисекундах();
		СуммаЦиклов = СуммаЦиклов + 1;

		Если СуммаЦиклов > 999 Тогда
			ПредЗамер = ЗамерВремени;
			ЗамерВремени = ТекущаяУниверсальнаяДатаВМиллисекундах();
			Загрузка = " " + РабочийЦикл / 10 + "% " + Цел(1000 * РабочийЦикл / (ЗамерВремени - ПредЗамер)) + " q/s " + Задачи.Количество() + " tasks";
			СуммаЦиклов = 0;
			РабочийЦикл = 0;
		КонецЕсли;

		ВремяЦикла = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЦикла;
		Если ВремяЦикла > 100 Тогда
			Сообщить("!morphserver ВремяЦикла=" + ВремяЦикла);
		КонецЕсли;
		НачалоЦикла = ТекущаяУниверсальнаяДатаВМиллисекундах();

		к = мЗадачи.Количество();
		Пока к > 0 И НЕ ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЦикла > 50 Цикл
			к = к - 1;
			структЗадача = мЗадачи.Получить(0);
			мЗадачи.Удалить(0);

			ЕстьРезультат = Ложь;
			РабочийЦикл = РабочийЦикл + 1;
			Попытка
				ЕстьРезультат = ВыполнитьЗадачу(структЗадача);
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
			Если ЕстьРезультат = Истина Тогда
				Попытка
					ОбратныйЗапрос = "";
					Если структЗадача.Запрос.Свойство("ОбратныйЗапрос", ОбратныйЗапрос) Тогда // возвращаем результат
						ОбратныйЗапрос.Вставить("РезультатДанные", Новый Структура("Ответ, Результат", структЗадача.Ответ, структЗадача.Результат));
						Если ПередатьДанные(ОбратныйЗапрос.Хост, ОбратныйЗапрос.Порт, ОбратныйЗапрос) = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						Сообщить("morphserver " + ТекущаяДата() + " time=" + (ТекущаяДата() - структЗадача.ВремяНачало) + Загрузка);
						структЗадача.Результат = Неопределено;
					КонецЕсли;
				Исключение
					Сообщить(ОписаниеОшибки());
				КонецПопытки;
				Задачи.Удалить(структЗадача.ИдЗадачи);
				//Сообщить("morphserver: всего задач " + Задачи.Количество());
				Продолжить;
			КонецЕсли;

			мЗадачи.Добавить(структЗадача);

		КонецЦикла;

		т = Таймаут;
		Пока Истина Цикл
			Соединение = TCPСервер.ПолучитьСоединение(т);
			Если Соединение = Неопределено Тогда
				Прервать;
			КонецЕсли;
			Соединения.Вставить(0, Соединение);
			т = 0;
		КонецЦикла;

		к = Соединения.Количество();
		Пока к > 0 Цикл
			к = к - 1;
			Соединение = Соединения.Получить(0);
			Соединения.Удалить(0);

			Если Соединение.Статус = "Успех" Тогда

				Попытка
					Запрос = Неопределено;
					Запрос = ДвоичныеДанныеВСтруктуру(Соединение.ПолучитьДвоичныеДанные());
				Исключение
					Сообщить("morphserver: " + ОписаниеОшибки());
				КонецПопытки;

				Если НЕ Запрос = Неопределено Тогда
					структЗадача = Новый Структура("ИдЗадачи, Запрос, Ответ, Результат, ВремяНачало", ПолучитьИД(), Запрос, Неопределено, Новый Структура(), ТекущаяДата());
					Задачи.Вставить(структЗадача.ИдЗадачи, структЗадача);
					мЗадачи.Добавить(структЗадача);
					//Сообщить("dataserver: всего задач " + Задачи.Количество());
				КонецЕсли;

				Соединение.Закрыть();
				Продолжить;

			ИначеЕсли Соединение.Статус = "Ошибка" Тогда

				Соединение.Закрыть();
				Продолжить;

			КонецЕсли;

			Соединения.Добавить(Соединение);

		КонецЦикла;

		ВремяЦикла = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЦикла;
		Если ВремяЦикла > 100 Тогда
			Сообщить("!morphserver ВремяЦикла=" + ВремяЦикла);
		КонецЕсли;
		Таймаут = ?(ВремяЦикла > 50, 0, 30);

	КонецЦикла;

	// Записать Биграммы
	ф = Новый ЗаписьТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Биграммы.txt"));
	Для каждого бгр Из Биграммы Цикл
		ф.ЗаписатьСтроку(бгр.Ключ);
		кол = бгр.Значение.Количество();
		ф.ЗаписатьСтроку(кол);
		Пока кол > 0 Цикл
			кол = кол - 1;
			ф.ЗаписатьСтроку(бгр.Значение[кол]);
		КонецЦикла;
	КонецЦикла;
	Сообщить("Записал Биграммы");
	ф.Закрыть();

	// Записать Грамматики
	ф = Новый ЗаписьТекста(ОбъединитьПути(ТекущийКаталог(), "morph", "Грамматики.txt"));
	Для каждого гр Из Грамматики Цикл
		ф.ЗаписатьСтроку(гр.Ключ);
		кол = гр.Значение.Количество();
		ф.ЗаписатьСтроку(кол);
		Пока кол > 0 Цикл
			кол = кол - 1;
			ф.ЗаписатьСтроку(гр.Значение[кол]);
		КонецЦикла;
	КонецЦикла;
	Сообщить("Записал Грамматики");
	ф.Закрыть();

	TCPСервер.Остановить();
	Сообщить("Завершил работу сервера морфологии.");

КонецПроцедуры

МоментЗапуска = ТекущаяУниверсальнаяДатаВМиллисекундах();
ОбработатьСоединения();
